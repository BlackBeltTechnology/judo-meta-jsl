ifndef::env-site,env-github[]
include::../_attributes.adoc[]
endif::[]
// Settings
:idprefix:
:idseparator: -
:icons: font
:KW: [purple]##**
:KWE: **##
:title-align: "right"
 
// TODO: schedule and init

= Transfer model
:page-toctitle:Transfer model


The transfer model acts as a front-facing interface masking the underlying entity model. The transfer model can hide the complexity and the details of the entity model and provides a simpler interface to the clients of the application. Or it can provide different views for different purposes, allowing changes in functionality without changing the underlying, often rigid data model, which is often created from very different aspects. In addition, the transfer model also reduces the dependence between the entity model and the outside world.

The main building blocks of the transfer model are transfer objects and actors. The following chapters describe these building blocks in detail.

[WARNING]
====
This chapter explains the transfer object, focusing on its role and excluding discussions on update or create. For information on modifying transfer objects, refer to the xref:09_actor.adoc[Access control] chapter.
====

== Transfer object

A transfer object is a container for a set of data fields that is transferred between the entity layer and any upper layers (e.g. user interface or external system). The transfer objects discussed in this sub-chapter are often referred to as "unmapped" transfer objects. This term is used because there are also "mapped" transfer objects, which will be discussed later.

[NOTE]
====
The term "transfer object" is a short version of "transfer object type". The correct term would be "transfer object type". However, to distinguish between the type and the instance, the object is called "instance of a transfer object" or "transfer object instance".
====

Transfer objects are simple non-persistent objects that carry data between our application and the outside world. The data members of a transfer object can be set and read at any stage of its life cycle.

To define an unmapped transfer object, use the `transfer` keyword.

*Syntax:*

[subs="quotes"]
----
[purple]#**transfer**# &lt;name> {
    &#x5B;member] ...
}
----

where the <name> is the name of the transfer object, and its data members are defined between `{` and `}`.

*Example:*

[source,jsl]
----
transfer PersonTransfer {
}
----

The above example declares the transfer object `PersonTransfer` with no members.


[judo-req="true",judo-req-id="REQ-SRV-003"]
=== Field

A transfer object may contain data members called fields. A field has an associated primitive domain type, in other words, the field is a typed element. Fields cannot store multiple primitive values (that is, collection), but only a single primitive value.

[NOTE]
====
If you want to have a collection of primitives, use a wrapper transfer object with just one primitive field, and use a relation to the collection of wrappers to store a collection of primitive values.
====

Use the `field` keyword to specify a field within a transfer object. The full list of modifiers can be found in the xref:appx_modifiers.adoc#transfer-field[Appendix].

*Syntax:*

[subs="quotes"]
----
[purple]#**field**# &lt;primitive> &lt;name>
    &#x5B;[purple]##**required:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**default:**##&lt;default>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>][purple]#**;**#
----

where <primitive> is the name of a domain model primitive and the <name> is the referable name of the field.

Setting the `required` modifier to `true` indicates that the value of the field cannot be undefined at the time the object is validated. When the transfer object is validated it will report fields that are required and having the value of UNDEFINED. The timing of validation is not discussed in this document. Validation can be called at any time from Java or script execution, or it can be called by the architecture (e.g. before the execution of a service).

Optionally, a `default` modifier can be added and a <default> value can be specified as an expression. This expression is evaluated when a new instance of the transfer object is created, and the field is set according to the return value of the expression. See xref:06_expression.adoc[Expression] later.

The `enabled` modifier specify the user's permission if they are allowed to modify the value of the field. Setting the `enabled` modifier to `true` allows the user to change the field and use the field as an input value. The `enabled` modifier expects an expression, and depending on the result of the expression the field can be disabled or enabled for value change. By default, fields are not allowed to change by the user (`enabled` is `false`). For more about permission check the xref:09_actor.adoc#permission-modifiers[Permission modifiers] chapter.

// See the validate command.

*Example:*

[source,jsl]
----
transfer PersonTransfer {
    field String firstName required enabled default:"John";
    field String lastName required enabled default:"Doe";
    field String midName enabled;
}
----

The example above defines a transfer object named PersonTransfer. This transfer object has three fields: `firstName` and `lastName` are two required strings, and `midName` is an optional string. After initialization, the values of the first name and last name will be set to "John" and "Doe" respectively. Moreover, users can change the field values because the `enabled` modifier is set to `true` for each field.

The explanation of further field modifiers can be found in the subsequent chapters.

// read-only fields?

=== Relation

A relation is a reference defined between two transfer objects. The concept closely resembles the relation concept described in xref:05_entity.adoc#relations[Relations] section of entities. The transfer object instance that holds the reference is considered the owner of the relation. As the owner, it has access to the instance being referred to by the reference.

Relations can be not only references to a single instance, but also collections. If the relation is a collection, the owner can access a set of instances at the same time.

Relations between transfer objects are inherently unidirectional. A relation can only be navigated in one direction: from the owner to the target of the relation. Navigation means that, at run-time, the owner of the relation gets the instance(s) of the target of the relation.

Use the `relation` keyword to specify a relation between two transfer objects. The full list of modifiers can be found in the xref:appx_modifiers.adoc#transfer-relation[Appendix].

*Syntax:*

[subs="quotes"]
----
[purple]#**relation**# &lt;transfer>&#x5B;[purple]##**[]**##] &lt;name>
    &#x5B;[purple]##**required:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**default:**##&lt;default>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>][purple]#**;**#
----

where <transfer> is the name of a transfer object to which the relation is targeted. The <name> is used to identify the relation within the owner, it is commonly referred to as role name.

The optional `[]` behind the <transfer> indicates that the relation is a collection rather than a single reference to one <transfer> instance. In other words, the cardinality of the relation is a collection.

Setting the `required` modifier to `true` indicates that the value of the relation cannot be undefined at the time the object is validated. When the transfer object is validated it will report relations that are required and having the value of UNDEFINED. The timing of validation is not discussed in this document. Validation can be called at any time from Java or script execution, or it can be called by the architecture (e.g. before the execution of a service). Note that the `required` modifier is not allowed for collections.

Optionally, you can add a `default` modifier and specify a <default> value as an expression. This <default> value expression is evaluated when a new instance of the transfer object is created, and the relation is set according to the expression's result. The <default> expression must select an entity or, if the relation is a collection, a list of entities. The transfer relation holds references to mapped transfer objects with selected mapped entities. See xref:06_expression.adoc[Expression] later. Note that a default value is applicable only when the relation's target is a mapped transfer object. Mapped transfer objects are explained in the subsequent chapter.

The `enabled` modifier specify the user's permission if they are allowed to modify the relation. Setting the `enabled` modifier to `true` allows the user to change the relation and use the relation as an input. The `enabled` modifier expects an expression, and depending on the result of the expression the relation can be disabled or enabled for change. By default, relations are not allowed to change by the user (`enabled` is `false`). For more about permission check the xref:09_actor.adoc#permission-modifiers[Permission modifiers] chapter.

*Example:*

[source,jsl]
----
transfer CustomerTransfer {
	relation OrderTransfer[] orders enabled;
}

transfer OrderTransfer {
}
----

The example above defines a relation. `orders` is defined within CustomerTransfer transfer object and refers to a list of OrderTransfer transfer object instances that belong to a particular customer. 

*Example:*

[source,jsl]
----
transfer AddressTransfer {
    field String line1 required;
    field String line2;
    field String City;
    field String ZipCode;
}

transfer PersonTransfer {
    relation AddressTransfer address required;
}
----

The second example defines the AddressTransfer transfer object with its fields, and each PersonTransfer instance must have an AddressTransfer.

The explanation of further relation modifiers can be found in the subsequent chapters.

[judo-req="true",judo-req-id="REQ-SRV-002"]
== Mapped transfer object

Data transformation between entities and transfer objects is a tedious and error-prone task. Mapped transfer objects are intended to facilitate this work and also carry data between our application and the outside world, but they can be linked to a specific entity instance. The link between the mapped transfer object instance and the entity instance is represented by a specific field of the mapped transfer object, called mapping field.

[IMPORTANT]
====
Understanding the concept of a *mapping field* is extremely important as you continue reading this document. The name "mapping field" comes from its role. In practice, the mapping field is a field in the mapped transfer object. This is a reference to the entity instance to which the transfer object is linked. This reference can be read at any time like any other field of the transfer object. An important consequence of the above is that the name of the mapping field cannot be used as a field name in the mapped transfer object.
====

Mapping is a configuration approach that copies entity field values to transfer object fields and vice versa in a pre-defined way, see more in <<Mapping configuration>> chapter.

To define a mapped transfer object, use the `transfer` keyword and specify an entity type as a parameter.

*Syntax:*

[subs="quotes"]
----
[purple]#**transfer**# &lt;name> [purple]##**(**##&lt;entity> &lt;mapping-field>[purple]##**)**## {
    &#x5B;member] ...
}
----

where the <name> is the name of the transfer object, and the member fields are defined between `{` and `}`. The <entity> is the entity type of the linked entity instance. The <mapping-field> is the name of the field that refers to the linked entity instance.

[NOTE]
====
There is an alternative syntax to define a mapped transfer object. This alternative is a bit more expressive, although it may be too verbose.

[subs="quotes"]
----
[purple]#**transfer**# &lt;name> [purple]#**maps**# &lt;entity> [purple]#**as**# &lt;mapping-field> &#x5B;{
    &#x5B;[purple]##**member**##] ...
}]
----
====

The example below shows a mapped transfer object.

*Example:*

[source,jsl]
----
entity Person {
    field String name;
}

transfer PersonTransfer(Person person) {
    field String name <= person.name;
}
----

The transfer object `PersonTransfer` is mapped to the `Person` entity. A particular instance of the Person entity is accessible as `person` within the scope of the PersonTransfer transfer object. The `person` field is a field of type `Person`, which is the mapping field.

The value of the mapping field (`person` in the example above) can be undefined, meaning the mapping field is not required. In some PersonTransfer instances, the mapping field may refer to a particular Person entity instance, and in others it may be undefined.


[judo-req="true",judo-req-id="REQ-SRV-005"]
=== Mapping configuration

Mapping configuration is a declarative description of how the fields of a transfer object read and/or write specific fields of the entity mapped by the transfer object. Mapping configuration can only be used for mapped transfer objects.

Use the `\<=` symbol in the field definitions to define the mapping configuration. The concept of mapping configuration is very similar to the concept of xref:05_entity.adoc#calculated-members[Calculated members] described for entities. The main difference is that mapped members can update their mapped entity's field in certain cases.

=== Mapped field

A mapped field is a simple field with a mapping configuration. The full list of modifiers can be found in the xref:appx_modifiers.adoc#transfer-field[Appendix].

*Syntax:*

[subs="quotes"]
----
[purple]#**field**# &lt;primitive> &lt;name> [purple]##**<=**## &lt;expression>
    &#x5B;[purple]##**required:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**default:**##&lt;default>]
    &#x5B;[purple]##**set:**##([purple]##**true**##|[purple]##**false**##)][purple]#**;**#
----

where <primitive> is the name of a domain model primitive.

The `\<=` symbol indicates that the value of the transfer field will be set to the value of the expression specified after `\<=`. This expression must return the same primitive type as the type of the field.

The `required`, `enabled`, and `default` modifiers are the same as described for simple xref:07_transfer.adoc#field[Transfer field].

If the `set` modifier is `true`, changes in the transfer field value will set the value of the mapped entity's field. It's important to note that the `set` modifier only works for expressions that select a non-calculated field.

*Example:*

[source,jsl]
----
entity SalesPerson {
    field String firstName;
    field String lastName;
}

transfer SalesPersonTransfer(SalesPerson salesperson) {
    field String fullName <= salesperson.firstName + " " + salesperson.lastName;
}
----

In the example above, transfer field value is set by concatenating the `firstName` and `lastName` fields of SalesPerson entity with a space in the middle. Note the `salesperson` reference in the expression which refers to the mapping field.

////
The <expression> is used whenever a transfer object or a collection of transfer objects are fetched from the persistent storage.
////

If the expression after the `\<=` symbol is a direct member selection of the mapped entity, in addition to setting the value of the transfer field, it writes the value of the transfer field to the entity field when the transfer object field changes. Direct member selection is a one step navigation as it is shown in the example below.

*Example:*

[source,jsl]
----
entity SalesPerson {
    field String firstName;
    field String lastName;
}

transfer SalesPersonTransfer(SalesPerson salesperson) {
    field String firstName <= salesperson.firstName set enabled;
    field String lastName <= salesperson.lastName set enabled;
}
----

In other words, the `\<=` symbol creates a read-only connection between the entity and the transfer fields, where the value of the transfer field takes the value of the expression. However, if the `set` modifier is `true`, it defines a read-write connection.

=== Mapped relation

So far, expressions returned primitive values. The following example shows a relation that is mapped to one of the entity's relations. The full list of modifiers can be found in the xref:appx_modifiers.adoc#transfer-relation[Appendix].

*Syntax:*

[subs="quotes"]
----
[purple]#**relation**# &lt;transfer>&#x5B;[purple]##**[]**##] &lt;name> [purple]##**<=**## &lt;expression>
    &#x5B;[purple]##**required:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**default:**##&lt;default>]

    &#x5B;[purple]##**choices:**##&lt;choices>]
    &#x5B;[purple]##**selector:**##&lt;transfer>]

    &#x5B;[purple]##**eager:**##([purple]##**true**##|[purple]##**false**##)]
    
    &#x5B;[purple]##**create:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**delete:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**update:**##([purple]##**true**##|[purple]##**false**##)][purple]#**;**#


----

where <transfer> is the name of a transfer object to which the relation is targeted. The <name> is used to identify the relation within the owner, it is commonly referred to as role name.

The optional `[]` behind the <transfer> indicates that the relation is a collection rather than a single reference to one <transfer> instance. In other words, the cardinality of the relation is a collection.

The `\<=` symbol indicates that the relation will be set to the value of the expression specified after `\<=`. This expression must select an entity or, if the relation is a collection, a list of entities. The transfer relation will hold references to mapped transfer objects with selected mapped entities. See xref:06_expression.adoc[Expression] later.

The `required`, `enabled`, and `default` modifiers are the same as described for simple xref:07_transfer.adoc#relation[Transfer relation].

*Example:*

[source,jsl]
----
entity SalesPerson {
    field String firstName;
    field String lastName;
    
	relation Lead[] leads;
}

entity Lead {
    field Integer value;
}

transfer LeadTransfer(Lead lead) {
    field Integer value <= lead.value;
}

transfer SalesPersonTransfer(SalesPerson salesperson) {
    field String fullName <= salesperson.firstName + " " + salesperson.lastName;
    relation LeadTransfer[] leads <= salesperson.leads;
}
----

The example defines a relation for `SalesPersonTransfer`. The relation `leads` is defined within SalesPersonTransfer and contains a list of LeadTransfer transfer objects that belong to a particular salesperson.

[IMPORTANT]
====
An important note, that the expression after the `\<=` must return an entity (or a collection of entities). The type of the returning entity must match to the mapped entity type of the transfer object. (e.g. it must be the same type or must be the subtype of the mapped entity type)
====

The `choices` modifier enables you to specify which entity instances can be added to or removed from the mapped relation. The <choices> expression must return an entity list. Instances not returned by the <choices> expression won't be allowed to be added. If you want to allow the addition of any instances, you can use choices as shown in this example:

*Example:*

[source,jsl]
----
transfer ProductTransfer(Product product) {
	relation ProductTransfer[] recommendations <= product.recommendations
	    choices:Product.all();
}
----

In this example, the choices modifier is set to Product.all(), which means any product instance can be added to the `recommendations` relation.

The `selector` modifier is used to specify the type of transfer object when the list of choices is requested. In other words, whenever the list of choices is requested, a list of mapped transfer objects is returned; the instances are specified by the <choices> expression, and the type of the transfer objects is specified by the `selector` modifier.

*Example:*

[source,jsl]
----
transfer ProductChoiceTransfer(Product product) {
    field String name <= product.name;
}

transfer ProductTransfer(Product product) {
	relation ProductTransfer[] recommendations <= product.recommendations
	    choices:Product.all()
	    selector:ProductChoiceTransfer;
}
----

The `create`, `delete` and `update` modifiers are discussed in the xref:09_actor.adoc[Access control] chapter.

=== Eager modifier

Only in the case of mapped transfer relations, the fetching strategy of the related transfer objects can be specified. Possible fetching strategies are the same as those seen in xref:05_entity.adoc#fetching-strategy[Entity] chapter.

* *Eager fetching* is a technique where related objects are fetched at the same time as the owner transfer object is retrieved from persistent storage.

* *Lazy fetching* is a technique where related objects are *not* fetched at the same time as the owner transfer object is retrieved from persistent storage.

By default, the the fetching strategy of mapped relations is lazy.

The default lazy fetching strategy of the transfer relation can be altered by applying the `eager` modifier. To apply the eager fetching strategy on a transfer relation, use the `eager` modifier with `true` value.

In the example below, the TransferOrder instances referenced by the `orders` mapped relation will be fetched together with the TransferCustomer instance.

*Example:*

[source,jsl]
----
transfer TransferCustomer(Customer customer) {
	relation TransferOrder[] orders <= customer.orders eager:true;
}
----


// TODO: create, delete, update flags

== Action

Actions, also known as methods or member functions, define the operations that a transfer object can perform.

The action has a body, which is the piece of code (script) that is executed when the action is invoked. Actions, in this context, encapsulate the behavior of an object. Actions can perform a wide range of tasks, from simple calculations to complex operations including creating, updating or deleting data or other real-time communications, and they can interact with the transfer object's fields and relations.

[NOTE]
====
Currently, the action body can only be specified in Java.
====

The action may return a computed value to its caller (its return value). This value is often referred to as output. In addition to the output, an action may also return different type of errors. The definition of errors is discussed in chapter Errors.

Use the `action` keyword to specify an action within a transfer object.

*Syntax:*

[subs="quotes"]
----
[purple]#**action**# &#x5B;&lt;output>|[purple]#**void**#] &lt;name> [purple]##**(**##&#x5B;&lt;parameter>] &#x5B;[purple]##**choices:**##&lt;choices>][purple]##**)**##
    &#x5B;[purple]##**throws**## &lt;error1> &#x5B;[purple]##**,**##&lt;error2>] ...]

    &#x5B;[purple]##**static:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**update:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**delete:**##([purple]##**true**##|[purple]##**false**##)][purple]#**;**#
----

where the <name> is the name of the action. Information can be passed to actions through a parameter, which acts as variable within the action. A <parameter> is specified after the action name within parentheses and consists of the name of a transfer object and a parameter name. An action can have at most one parameter.

The <output> of the action can be a transfer object type or a <<Union>>. The keyword `void` indicates that the action does not return any data.

The optional `throws` keyword must be followed by one ore more <error> names separated by commas. The list of errors declares which errors can be thrown from the action. See more about errors in xref:08_error.adoc[Error] chapter.

The `static` modifier with `true` value in action declarations is used to create an action that belongs to the transfer object type rather than to an instance of the transfer object type. Consequently, one can invoke a static action on the transfer object type directly without creating an instance. Inside a static action, you cannot access fields or call other instance actions directly, as static actions are not associated with a specific instance.

The `enabled` modifier specifies the user's permission to call the action. Setting the `enabled` modifier to `true` allows the user to call the action. The `enabled` modifier expects an expression, and depending on the result of the expression, the action can be disabled or enabled for invocation. By default, actions are allowed to call by the user (`enabled` is `true`). For more about permission check the xref:09_actor.adoc#permission-modifiers[Permission modifiers] chapter.

The `choices` modifier enables you to specify which transfer object instances can be selected as an input parameter. Transfer object instances that are mapped to entities not listed by the choices expression won't be allowed to be added as parameter. Therefore, the <choices> expression must return a list of entities. 

*Example:*

[source,jsl]
----
transfer ThankYouTransfer {
	field String message <= "Thank you for your order. Please check other products at";
}

transfer CouponTransfer {
    field String code;
}

transfer CartTransfer maps Customer as customer {
	// set the status of the order to OrderStatus#ORDERED
	action ThankYouTransfer order(CouponTransfer coupon);
}
----

In the above example, the CartTransfer mapped transfer object has an action called `order`. Its input is a CouponTransfer unmapped transfer object containing the discount coupon code, if any. The `order` action returns a ThankYouTransfer unmapped transfer object.

[judo-req="true",judo-req-id="REQ-SRV-013"]
=== Union

A union is a type to hold different type of transfer objects. Unions can be used only as the return type of actions. A union must have at least two members, but only one member can exist at any given time.

Unions cannot be organized into a collection.

*Syntax:*

[subs="quotes"]
----
[purple]#**union**# &lt;name> [purple]##**<**## &lt;transfer1>[purple]##** | **##&lt;transfer2> &#x5B;[purple]##**| **##&lt;transfer3> ]...[purple]#**>;**#
----

where the <name> is the name of the union. Between `<` and `>` is a list of transfer objects saparated by the pipe character (`|`).

*Example:*

[source,jsl]
----
union Employee <SalesPersonTransfer|DeveloperTransfer>;
----

In the example above, we define the `Employee` union, which contains either a SalesPersonTransfer or a DeveloperTransfer transfer object. Within the body of an action that returns `Employee`, it can be decided whether a `SalesPersonTransfer` or a `DeveloperTransfer` object is returned.

== Lifecycle

Understanding the concept and behavior of transfer objects is essential when creating, updating, saving, deleting, and working with them. The lifetime of transfer objects is the time that elapses between the creation and destruction of a particular instance. The events that occur during their lifetime are described by the lifecycle process.

=== Unmapped transfer object lifecycle

Unmapped transfer objects are objects used for data transfer but are not associated with any storage mechanism. Consequently, their lifecycle is straightforward, consisting of just one stage: from creation to elimination when they are no longer referenced. Here's how the lifecycle of unmapped transfer objects works:

.Unmapped transfer object lifecycle
[plantuml, target=diagram-uto-lifecycle, format=png, align="center"]
....
@startuml lifecycle
skinparam nodesep 50
skinparam ranksep 10
skinparam shadowing false

skinparam padding 2
skinparam roundCorner 8

skinparam state {
    FontColor<<White>> white
    BackgroundColor<<White>> white
    BorderColor<<White>> white

    BackgroundColor white|#f5eece
    BorderColor #grey
    ArrowColor #black
    FontSize 13
    FontStyle bold
}

[*] -right-> Initialized : initialize
Initialized -right-> [*]

@enduml
....

* Once created, these objects are populated with data through their initialize event handler. More details on this event can be found later.

* After their purpose has been served, and the data has been transferred, unmapped transfer objects often become immediately eligible for garbage collection. When all references to an unmapped transfer object are lost, it becomes eligible for garbage collection.

Their simple lifecycle reflects their role as transient objects used for communication between different parts of the software, with an emphasis on efficiency and minimal memory usage. Garbage collection ensures that these objects are cleaned up when they are no longer needed, contributing to effective memory resource management.

=== Mapped transfer object lifecycle

Mapped transfer objects are connected to the storage mechanism through entity mapping. As a result, their lifecycle is more complicated than the lifecycle of unmapped transfer objects.

The following diagram depicts the lifecycle of mapped transfer objects.

.Mapped transfer object lifecycle
[plantuml, target=diagram-mto-lifecycle, format=png, align="center"]
....
@startuml lifecycle
skinparam nodesep 50
skinparam ranksep 10
skinparam shadowing false

skinparam padding 2
skinparam roundCorner 8

skinparam state {
    FontColor<<White>> white
    BackgroundColor<<White>> white
    BorderColor<<White>> white

    BackgroundColor white|#f5eece
    BorderColor #grey
    ArrowColor #black
    FontSize 13
    FontStyle bold
}

state Persisted
state Modified

state Start <<White>> {
    [*] -right-> Initialized : initialize
    state Initialized : Detached state
}


[*] -down-> Persisted : fetch
Initialized -right-> Persisted : create
Persisted ---> Modified : change
Modified ---> Persisted : update
Modified ---> Persisted : fetch
Persisted -right-> [*] : delete

Modified: Detached state

@enduml
....

A mapped transfer object can go through three stages during its lifecycle:

* *Initialized:* This is where the object begins its lifecycle. In this state, the object's fields have their default values. The object has not yet been persisted, so its mapping field is undefined. Since the fields of the mapped transfer object do not match the fields of the mapping entity (as it does not yet exist), we refer to this as a detached state.

* *Persisted:* This state represents an object in a persisted or saved state, indicating that its fields are stored in the mapping entity. The mapped transfer object has already been persisted, so its mapping field is defined.

* *Modified:* The Modified state represents an object that has been altered, but the modifications have not yet been saved to the fields of the mapping entity. The mapping entity already exists, so the mapped transfer object's mapping field is defined. Since the fields of the mapped transfer object do not match the fields of the mapping entity, we also refer to this as a detached state.

A transition refers to a change in the object's state that occurs as a result of a specific event. Events are shown next to transition arrows on the diagram. The following events can occur to a mapped transfer object:

* The *initialize* event represents the action of initializing an object, causing it to transition to the initialized state. It sets the initial field values of the object.

* The *create* event signifies the action of saving an object's state to the storage. It transitions the object from the initialized state to the persisted state.

* The *fetch* event indicates the retrieval of data from the persistent storage. It transitions the object into the persisted state where it holds the fetched data.

* The *change* event represents an action where the mapped transfer object's fields changed. This leads to a transition from the persisted state to the modified state, indicating that changes have been made but not yet saved.

* The *update* event is associated with saving the modifications made to a transfer object. It causes the object to transition from the modified state back to the persisted state, reflecting that the changes have been persisted.

* The *delete* event is used to indicate the removal or deletion of an object. It results in the object transitioning to an end state and its mapped entity being removed from the persistent storage entirely.

== Event handlers

Event handlers are blocks of code that are designed to respond to specific events. Their primary purpose is to define how the system should react when particular events occur. The list of events that may occur can be found in the lifecycle description of transfer objects.

Event handlers can be used to customize the behavior of the application by executing code before, after, or instead of the default event handling mechanism.

* *Before-event* handlers run before the default event handler and are useful for setting up conditions or performing validations.

* *After-event* handlers execute after the default event handler has completed its task. They are suitable for cleanup, logging, or triggering additional actions.

* *Instead-of-event* handlers replace the default event handler with custom code, allowing complete control over event processing.

The following event handlers can be defined for transfer objects.

[judo-req="true",judo-req-id="REQ-SRV-004"]
=== Initialize

The primary goal of the initialize event is to set default data for a mapped transfer object. When the initialize event occurs, the default event handler sets the initial state and assigns values to the members of the transfer object as specified in their default values.

Optionally, initialize event handlers can be specified for transfer objects. The initialize event handlers are automatically called before, after and when a new transfer object is constructed and can be used to set default values of the fields.

Initialize event handlers can be defined both for unmapped and mapped transfer objects.

[NOTE]
====
Currently, the body of the initialize event handlers can only be defined in Java.
====

*Syntax:*

[subs="quotes"]
----
[purple]#**event**# [purple]#**initialize**# &lt;name>&#x5B;[purple]##**()**##]
    &#x5B;[purple]##**on:**##([purple]##**before**##|[purple]##**after**##)][purple]##**;**##
----

In this syntax, <name> represents the identifier for the event handler.

When employing the `on` modifier with `before` value, the initialize event handler executes prior to the actual initialization process. This handler provides an opportunity to perform specific tasks or validations before the default initialization occurs. At this stage, the object has not yet been fully initialized, allowing for adjustments or customizations to be made before the object is formally set up. This can be useful for applying business rules or performing any necessary checks. Note that the member values will be `UNDEFINED` during execution.

Conversely, if employing the `on` keyword with `after` value, the handler is triggered after the object has been successfully initialized. This handler allows for additional actions to be taken once the default initialization process is complete. It's useful for tasks such as updating related entities, triggering notifications, or performing any post-initialization operations. At this point, the object is already initialized with its initial values.

If no modifier is utilized, the event handler replaces the standard mechanism for initializing objects. Within this handler, developers have the flexibility to define custom procedures for object initialization, including the option to set field and relation values of the transfer object. In this case, you should provide the default values within the handler. If there are no default value settings in the handler, members will have `UNDEFINED` values after initialization, even if default values are specified in the member declarations.

*Example:*

[source,jsl]
----
transfer PersonTransfer(Person person) {
    field String firstName required default:"FN";
    field String lastName required default:"LN";
    field String midName;
    
    event initialize beforeInit on:before;
    event initialize afterInit on:after;
    event initialize insteadInit;
}
----

=== Create

The create event occurs when a mapped transfer object is persisted for the first time. The create event encompasses three distinct types of handlers: after, before, and instead. When a mapped transfer object undergoes its initial persistence, the before event handler is invoked before the mapping entity is created. This allows for the execution of specific actions or processes prior to the default creation process. Subsequently, the after event handler is triggered after the creation of the mapping entity, providing an opportunity to perform additional tasks or modifications once the default creation is complete. In contrast, the instead event handler is an alternative mechanism; when specified, it supersedes the default creation process entirely. If the instead event handler is employed, it becomes responsible for executing the necessary operations of the standard creation process.

A create event handler can only be defined for mapped transfer objects.

[NOTE]
====
Currently, the body of the create event handler can only be defined in Java.
====

*Syntax:*

[subs="quotes"]
----
[purple]#**event**# [purple]#**create**# &lt;name>&#x5B;[purple]##**()**##]
    [purple]##**on:**##([purple]##**before**##|[purple]##**after**##)[purple]##**;**##
----

or

[subs="quotes"]
----
[purple]#**event create**# &lt;name>&#x5B;[purple]##**(**##&lt;transfer> &lt;parameter>[purple]##**)**##][purple]##**;**##
----

In this syntax, <name> represents the identifier for the event handler.

When employing the `on` modifier with `before` value, the create event handler executes prior to the actual object creation. This handler provides an opportunity to perform specific tasks or validations prior to the default creation process. At this stage, the object has not yet been persisted in the storage, allowing for adjustments or customizations to be made before the object is formally created. This can be useful for applying business rules, or performing any necessary checks.

Conversely, if employing the `on` keyword with `after` value, the handler is triggered after the object has been successfully created and persisted. This handler allows for additional actions to be taken once the default creation process is complete. It's useful for tasks such as updating related entities, triggering notifications, or performing any post-creation operations. At this point, the object is already stored in the persistent storage with its values.

If no modifier is utilized, the event handler replaces the standard mechanism for creating objects. Within this handler, developers have the flexibility to define custom procedures for persisting object, including the option to set field and relation values. In this case the create event handler can optionally accept a transfer object parameter. The fields of the parameter can be utilized when the transfer object is saved for the first time. The parameter enables differentiation between the creation and update parameters of the transfer object.

*Example:*

[source,jsl]
----
transfer PersonTransfer(Person person) {
    field String firstName required;
    field String lastName required;
    field String midName;
    
    event create beforeCreate on:before;
    event create afterCreate on:after;
    event create insteadCreate;
}
----

=== Fetch

The primary purpose of the fetch event is to retrieve data from persistent storage. When the fetch event occurs, the field values of the mapping entity are copied to the fields of the mapped transfer object.

The fetch event occurs when a mapped transfer object is retrieved from persistent storage. The fetch event also encompasses three distinct types of handlers: after, before, and instead. When a mapped transfer object undergoes retrieval, the before event handler is invoked before the data is obtained. This allows for the execution of specific actions or processes prior to the default retrieval process. Subsequently, the after event handler is triggered after the data retrieval is complete, providing an opportunity to perform additional tasks or modifications once the default retrieval is finished. In contrast, the instead event handler is an alternative mechanism; when specified, it supersedes the default retrieval process entirely. If the instead event handler is employed, it becomes responsible for executing the necessary operations instead of the standard retrieval process.

A fetch event handler can only be defined for mapped transfer objects.

[NOTE]
====
Currently, the body of the fetch event handler can only be defined in Java.
====

[subs="quotes"]
----
[purple]#**event**# [purple]#**fetch**# &lt;name>&#x5B;[purple]##**()**##]
    &#x5B;[purple]##**on:**##([purple]##**before**##|[purple]##**after**##)][purple]##**;**##
----

In this syntax, <name> represents the identifier for the event handler.

When employing the `on` modifier with `before` value, the fetch event handler executes prior to the actual data retrieval. This handler provides an opportunity to perform specific tasks or validations prior to the default retrieval process. At this stage, the object has not yet been retrieved from the storage, allowing for adjustments or customizations to be made before the object is formally fetched. This can be useful for applying business rules or performing any necessary checks.

Conversely, if employing the `on` keyword with `after` value, the handler is triggered after the object has been successfully retrieved. This handler allows for additional actions to be taken once the default retrieval process is complete. It's useful for tasks such as updating related entities, triggering notifications, or performing any post-retrieval operations. At this point, the object is already retrieved from persistent storage with its values.

If no modifier is utilized, the event handler replaces the standard mechanism for fetching objects. Within this handler, developers have the flexibility to define custom procedures for retrieving objects, including the option to set field and relation values of the transfer object.

*Example:*

[source,jsl]
----
transfer PersonTransfer(Person person) {
    field String firstName required;
    field String lastName required;
    field String midName;
    
    event fetch beforeFetch on:before;
    event fetch afterFetch on:after;
    event fetch insteadFetch;
}
----


=== Update

The primary objective of the update event is to modify data in persistent storage. When the update event occurs, the field values of the mapped transfer object are used to update the corresponding fields of the mapping entity.

The update event takes place when a mapped transfer object is modified and persisted to the storage. It encompasses three distinct types of handlers: after, before, and instead. When a mapped transfer object undergoes an update, the before event handler is invoked before the data is modified. This allows for the execution of specific actions or processes prior to the default update process. Subsequently, the after event handler is triggered after the data modification is complete, providing an opportunity to perform additional tasks or modifications once the default update is finished. In contrast, the instead event handler is an alternative mechanism; when specified, it supersedes the default update process entirely. If the instead event handler is employed, it becomes responsible for executing the necessary operations instead of the standard update process.

An update event handler can only be defined for mapped transfer objects.

[NOTE]
====
Currently, the body of the update event handler can only be defined in Java.
====

*Syntax:*

[subs="quotes"]
----
[purple]#**event**# [purple]#**update**# &lt;name>&#x5B;[purple]##**()**##]
    &#x5B;[purple]##**on:**##([purple]##**before**##|[purple]##**after**##)][purple]##**;**##
----

In this syntax, <name> represents the identifier for the event handler.

When employing the `on` modifier with `before` value, the update event handler executes prior to the actual data modification. This handler provides an opportunity to perform specific tasks or validations prior to the default update process. At this stage, the object has not yet been modified in the storage, allowing for adjustments or customizations to be made before the object is formally updated. This can be useful for applying business rules or performing any necessary checks.

Conversely, if employing the `on` keyword with `after` value, the handler is triggered after the object has been successfully updated. This handler allows for additional actions to be taken once the default update process is complete. It’s useful for tasks such as updating related entities, triggering notifications, or performing any post-update operations. At this point, the object is already updated in persistent storage with its new values.

If no modifier is utilized, the event handler replaces the standard mechanism for updating objects. Within this handler, developers have the flexibility to define custom procedures for updating objects, including the option to set field and relation values of the transfer object.


*Example:*

[source,jsl]
----
transfer PersonTransfer(Person person) {
    field String firstName required;
    field String lastName required;
    field String midName;
    
    event fetch beforeUpdate on:before;
    event fetch afterUpdate on:after;
    event fetch insteadUpdate;
}
----

=== Delete

The primary objective of the delete event is to remove data from persistent storage. When the delete event occurs, the corresponding mapping entity is removed, and its field values are no longer present.

The delete event takes place when a mapped transfer object is marked for deletion and removed from persistent storage. It encompasses three distinct types of handlers: after, before, and instead. When a mapped transfer object undergoes deletion, the before event handler is invoked before the data is removed. This allows for the execution of specific actions or processes prior to the default deletion process. Subsequently, the after event handler is triggered after the data removal is complete, providing an opportunity to perform additional tasks or modifications once the default deletion is finished. In contrast, the instead event handler is an alternative mechanism; when specified, it supersedes the default deletion process entirely. If the instead event handler is employed, it becomes responsible for executing the necessary operations instead of the standard deletion process.

A delete event handler can only be defined for mapped transfer objects.

[NOTE]
====
Currently, the body of the delete event handler can only be defined in Java.
====

*Syntax:*

[subs="quotes"]
----
[purple]#**event**# [purple]#**delete**# &lt;name>&#x5B;[purple]##**()**##]
    &#x5B;[purple]##**on:**##([purple]##**before**##|[purple]##**after**##)][purple]##**;**##
----

In this syntax, <name> represents the identifier for the event handler.

When employing the `on` modifier with `before` value, the delete event handler executes prior to the actual data removal. This handler provides an opportunity to perform specific tasks or validations prior to the default deletion process. At this stage, the object has not yet been removed from the storage, allowing for adjustments or customizations to be made before the object is formally deleted. This can be useful for applying business rules or performing any necessary checks.

Conversely, if employing the `on` keyword with `after` value, the handler is triggered after the object has been successfully deleted. This handler allows for additional actions to be taken once the default deletion process is complete. It’s useful for tasks such as updating related entities, triggering notifications, or performing any post-deletion operations. At this point, the object is already removed from persistent storage.

If no modifier is utilized, the event handler replaces the standard mechanism for deleting objects. Within this handler, developers have the flexibility to define custom procedures for deleting objects.

*Example:*

[source,jsl]
----
transfer PersonTransfer(Person person) {
    field String firstName required;
    field String lastName required;
    field String midName;

    event delete beforeDelete on:before;
    event delete afterDelete on:after;
    event delete insteadDelete;
}
----


////
For later use

== Constraint

A constraint represents some restriction related to a transfer object. A constraint is specified by a logical expression which must evaluate to a true or false. Constraint must be satisfied (i.e. evaluated to true) by a correct use of the system.

One transfer object may contain multiple constraints that must be satisfied. The order in which the multiple constraints are evaluated is the same as the order in which the constraints in the jsl file are declared.

Use the `constraint` keyword to specify a restriction on a transfer object. The syntax of constraint is the same as it is defined at entities.

*Example:*

[source,jsl]
----
error NameIsTooShort {
    field required String name
}

transfer Person {
    field required String firstName
    field required String lastName
    field String midName = ""
    
    constraint self.firstName!length() + self.lastName!length() > 4 \
    onerror NameIsTooShort(name = self.firstName + " " + self.lastName)
}
----

Constraints defined in transfer objects are not evaluated automatically. To check a transfer object instance against its constraints, use the `validate` command. See commands later.
////
