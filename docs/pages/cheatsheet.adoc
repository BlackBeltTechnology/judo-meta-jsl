ifndef::env-site,env-github[]
include::../_attributes.adoc[]
endif::[]
// Metadata
:judo-reqprefix: REQ-INTR
// Settings
:idprefix:
:idseparator: -
:icons: font
:KW: [purple]##**
:KWE: **##
:source-highlighter: highlight.js

= Cheatsheet

// TODO: enum is missing

== Model

Every model file must start with the definition of the model name.
The `model` keyword is used to start a model definition.

*Syntax:*

[subs="quotes"]
----
[purple]#**model**# &lt;name>[purple]#**;**#
----

*Example:*

[source,jsl]
----
model shop;
----

[source,jsl]
----
model judo::example::shop;
----

== Import

The `import` keyword is used to access elements of another model in the current model.

*Syntax:*

[subs="quotes"]
----
[purple]#**import**# &lt;model> &#x5B;[purple]##**as**## &lt;alias>][purple]#**;**#
----

*Example:*

[source,jsl]
----
import judo::types;
----

If no alias is specified, the elements of the imported model can be accessed by their simple name or by their fully qualified name.

[source,jsl]
----
import judo::types as types;
----

If an alias is specified, the elements of the imported model can be accessed by their name with the alias prefix or by their fully qualified name.

== Type

The primitive data types in the model must be specified with the `type` keyword.

*Syntax:*

[subs="quotes"]
----
[purple]#**type**# &lt;basetype> &lt;name> &#x5B;&lt;modifier name>[purple]#**:**#&lt;modifier value>] ... [purple]#**;**#
----

*Example:*

[source,jsl]
----
type boolean Boolean;
type date Date;
type time Time;
type timestamp Timestamp;
type numeric Integer precision:9 scale:0;
type numeric Long precision:15 scale:0;
type string String min-size:0 max-size:4000;
----

== Entity

The persistent data of a model is described in the form of entity types.

*Syntax:*

[subs="quotes"]
----
[purple]#**entity**# &lt;name> &#x5B;[purple]##**extends**## &lt;entity>[purple]##**,**## ...] 
    &#x5B;[purple]##**abstract:**##([purple]##**true**##|[purple]##**false**##)]
[purple]##**{**##
    [member] ...
[purple]#**}**#
----

*Example:*

[source,jsl]
----
entity Person extends Customer, User abstract;
----

=== Entity stored primitive field

An entity instance can store primitive values in its fields.

*Syntax:*

[subs="quotes"]
----
([purple]#**field**#|[purple]#**identifier**#) &lt;primitive> &lt;name>
    &#x5B;[purple]##**required:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**default:**##&lt;default>][purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name  | Modifier type                       | Meaning
|required       | boolean literal                     | If he field value must be specified.
|default        | expression returns primitive value  | The default value of the field.
|====================

*Example:*

[source,jsl]
----
field String name required default:"John Doe";
----

=== Entity stored composition

A composite field is an entity member whose type is an entity.

*Syntax:*

[subs="quotes"]
----
[purple]#**field**# &lt;entity>&#x5B;[purple]##**[]**##] &lt;name>
    &#x5B;[purple]##**required:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**eager:**##([purple]##**true**##|[purple]##**false**##)][purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name  | Modifier type     | Meaning
|required       | boolean literal   | If the field value must be specified.
|eager          | boolean literal   | If the fetching strategy is eager.
|====================

*Example:*

[source,jsl]
----
field Address address required eager;
----

[source,jsl]
----
field Address[] addresses;
----

=== Entity stored relation

Relation is a reference to an other entity or set of entities defined within an entity.

*Syntax:*

[subs="quotes"]
----
[purple]#**relation**# &lt;entity>&#x5B;[purple]##**[]**##] &lt;name>
    &#x5B;[purple]##**required:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**default:**##&lt;default>]
    &#x5B;[purple]##**opposite:**##&lt;opposite> | [purple]##**opposite-add:**##&lt;opposite>&#x5B;[purple]##**[]**##]]
    &#x5B;[purple]##**eager:**##([purple]##**true**##|[purple]##**false**##)][purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name  | Modifier type               | Meaning
|required       | boolean literal             | If the relation value must be specified, the modifier cannot be used for collections.
|default        | expression returns entities | The default value of the field.
|opposite       | relation name               | Create a bidirectional relation.
|opposite-add   | relation name               | Create a bidirectional relation with injection.
|eager          | boolean literal             | If the fetching strategy is eager.
|====================

*Example:*

[source,jsl]
----
relation Order[] orders eager opposite:customer;
----

[source,jsl]
----
relation Product product required opposite-add:orderItems[];
----

=== Entity calculated primitive

Calculated primitives can be read as if they were stored fields, but they have an expression that returns their value.

*Syntax:*

[subs="quotes"]
----
[purple]#**field**# &lt;primitive> &lt;name> [purple]##**<=**## &lt;expression>[purple]#**;**#
----

*Example:*

[source,jsl]
----
field String fullName <= self.firstName + " " + self.lastName;
----

=== Entity calculated relation

Calculated relations can be read as if they were stored relations, but they have an expression that returns their value.

*Syntax:*

[subs="quotes"]
----
[purple]#**relation**# &lt;entity>&#x5B;[purple]##**[]**##] &lt;name> [purple]##**<=**## &lt;expression>
    &#x5B;[purple]##**eager:**##([purple]##**true**##|[purple]##**false**##)][purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type               | Meaning
|eager          | boolean literal              | If the fetching strategy is eager.
|====================

*Example:*

[source,jsl]
----
relation Order cart <= self.orders.filter(o | o.status == OrderStatus#OPEN).any();
----

== Query

A query is a request within the scope of the model that retrieves a primitive or entities.

*Syntax:*

[subs="quotes"]
----
[purple]#**query**# (&lt;primitive>|&lt;entity>&#x5B;[purple]##**[]**##]) &lt;name> &#x5B;[purple]##**on**## &lt;owner>] [purple]##**(**##&#x5B;&lt;primitive> &lt;argument> [purple]##**=**## &lt;default>[purple]##,## ...][purple]##**)**## [purple]##**<=**## &lt;expression>[purple]#**;**#
----

*Example:*

[source,jsl]
----
query Integer NumOfCustomers(String country) <=
        Customer.all().
        filter(c | c.address.country == country).
        size();
----

[source,jsl]
----
query Order[] ordersBetween(Integer min = 0, Integer max = 100) on Customer <=
    self.orders.filter(o | o.price > min and  o.price < max);
----

== Transfer

A transfer object is a container for a set of data fields that is transferred between the service layer and any upper layers (e.g. user interface or external system).

*Syntax:*

[subs="quotes"]
----
[purple]#**transfer**# &lt;name> &#x5B;[purple]##**(**##&lt;entity> &lt;mapping-field>[purple]##**)**##] {
    &#x5B;member] ...
} [purple]#**;**#
----

or

[subs="quotes"]
----
[purple]#**transfer**# &lt;name> &#x5B; [purple]#**maps**# &lt;entity> [purple]#**as**# &lt;mapping-field> ] {
    &#x5B;member] ...
} [purple]#**;**#
----

*Example:*

[source,jsl]
----
transfer ProductTransfer(Product product) {
	field String name <= product.name;
	field String price <= product.price.asString() + " HUF";
}
----

=== Initialize event

The initializer is automatically called when a new transfer object is created and can be used to set default values of the fields.

*Syntax:*

[subs="quotes"]
----
[purple]#**event**# ([purple]#**before**#|[purple]#**after**#|[purple]#**instead**#) [purple]#**initialize**# &lt;name>&#x5B;[purple]##**()**##][purple]##**;**##
----

*Example:*

[source,jsl]
----
transfer PersonTransfer {
    event instead initialize insteadInit;
}
----

=== Create event

The create event handler is automatically called when a mapped transfer object is persisted for the first time.

*Syntax:*

[subs="quotes"]
----
[purple]#**event**# ([purple]#**before**#|[purple]#**after**#) [purple]#**initialize**# &lt;name>&#x5B;[purple]##**()**##][purple]##**;**##
----

or

[subs="quotes"]
----
[purple]#**event instead create**# &lt;name>&#x5B;[purple]##**(**##&lt;transfer> &lt;parameter>[purple]##**)**##][purple]##**;**##
----

=== Fetch event

The fetch event handler is automatically called when a transfer object is retrieved from persistent storage. 

*Syntax:*

[subs="quotes"]
----
[purple]#**event**# ([purple]#**before**#|[purple]#**after**#|[purple]#**instead**#) [purple]#**fetch**# &lt;name>&#x5B;[purple]##**()**##][purple]##**;**##
----

=== Update event

The update event handler is automatically called when a transfer object is persisted. 

*Syntax:*

[subs="quotes"]
----
[purple]#**event**# ([purple]#**before**#|[purple]#**after**#|[purple]#**instead**#) [purple]#**update**# &lt;name>&#x5B;[purple]##**()**##][purple]##**;**##
----

=== Delete event

The delete event handler is automatically called when a transfer object is deleted. 

*Syntax:*

[subs="quotes"]
----
[purple]#**event**# ([purple]#**before**#|[purple]#**after**#|[purple]#**instead**#) [purple]#**delete**# &lt;name>&#x5B;[purple]##**()**##][purple]##**;**##
----

=== Transfer primitive field

A transfer object may contain data descriptions called primitive fields. A primitive field can store only a single primitive value.

*Syntax:*

[subs="quotes"]
----
[purple]#**field**# &#x5B;[purple]##**required**##] &lt;primitive> &lt;name> &#x5B;[purple]##**<=**## &lt;expression>]
    &#x5B;[purple]##**input:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**default:**##&lt;default>][purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type                     | Meaning
|input          | boolean literal                    | If the field is used for conveying a value into the system.
|default        | expression returns primitive value | The default value of the field.
|====================

*Example:*

[source,jsl]
----
field Integer price <= customer.cart.price;
----

=== Transfer relation

A transfer object may contain data descriptions called relations. Relation is a reference to an other transfer object or set of objects.

*Syntax:*

[subs="quotes"]
----
[purple]#**relation**# &#x5B;[purple]##**required**##] &lt;transfer>&#x5B;[purple]##**[]**##] &lt;name> &#x5B;[purple]##**<=**## &lt;expression>]
    &#x5B;[purple]##**eager:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**choices:**##&lt;choices>]
    &#x5B;[purple]##**default:**##&lt;default>]
    &#x5B;[purple]##**create:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**update:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**delete:**##([purple]##**true**##|[purple]##**false**##)][purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type               | Meaning
|eager          | boolean literal              | If the fetching strategy is eager.
|choices        | expression returns entities  | The possible list of transfers to add.
|default        | expression returns entities  | The default list of transfers.
|create         | boolean literal              | If create is allowed on this relation.
|update         | boolean literal              | If update is allowed on this relation.
|delete         | boolean literal              | If delete is allowed on this relation.
|====================

*Example:*

[source,jsl]
----
relation OrderItemTransfer[] items <= customer.cart.orderItems delete:true;
----

=== Transfer action

The action is a piece of code (currently Java implementation) that is executed when the action is invoked. An action may receive input as defined in its parameter.

*Syntax:*

[subs="quotes"]
----
[purple]#**action**# &#x5B;[purple]##**static**##] &#x5B;&lt;type>|&lt;union>|[purple]#**void**#] &lt;name> [purple]##**(**##&#x5B;&lt;parameter>][purple]##**)**## &#x5B;[purple]##**throws**## &lt;error1> &#x5B;, &lt;error2>] ...] [purple]#**;**#
----

*Example:*

[source,jsl]
----
action void order();
----

////
=== Transfer submit

The submit is a piece of code (currently Java implementation) that is executed when the submit is invoked. The submit receives the primitive fields and relations annotated with @Embedded as its input. If there is no body defined for the submit, the default behavior is saving the transfer object's field to the mapped entity.

*Syntax:*

[subs="quotes"]
----
[purple]#**submit**# &#x5B;&lt;type>|&lt;union>|[purple]#**void**#] &lt;name> &#x5B;[purple]##**throws**## &lt;error1> &#x5B;, &lt;error2>] ...] [purple]#**;**#
----
////

== View

A view is a transfer object that also defines the visual representation of data.

*Syntax:*

[subs="quotes"]
----
[purple]#**view**# &lt;name> &#x5B;[purple]##**(**##&lt;entity> &lt;mapping-field>[purple]##**)**##] &#x5B;{
    &#x5B;member] ...
}] [purple]#**;**#
----

or

[subs="quotes"]
----
[purple]#**view**# &lt;name> &#x5B; [purple]#**maps**# &lt;entity> [purple]#**as**# &lt;mapping-field> ] &#x5B;{
    &#x5B;member] ...
}] [purple]#**;**#
----

*Example:*

[source,jsl]
----
view CartView maps Customer as customer {
	table OrderItemRow[] items <= customer.cart.orderItems;
	field Integer price <= customer.cart.price;
	action ThankYouView order();
}
----

=== Initialize event

See <<Initialize event>> of transfer.

=== Create event

See <<Create event>> of transfer.

=== Fetch event

See <<Fetch event>> of transfer.

=== Update event

See <<Update event>> of transfer.

=== Delete event

See <<Delete event>> of transfer.

=== View primitive field

A view primitive field can represent a single primitive value on the view.

*Syntax:*

[subs="quotes"]
----
[purple]#**field**# &#x5B;[purple]##**required**##] &lt;primitive> &lt;name> &#x5B;[purple]##**<=**## &lt;expression>]
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>]
    &#x5B;[purple]##**width:**##&lt;width>]
    &#x5B;[purple]##**input:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**default:**##&lt;default>][purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the field.
|icon           | string literal        | The name of the icon to show for the field.
|enabled        | boolean expression    | If the field is enabled for editing.
|hidden         | boolean expression    | If the field is shown to the user.
|width          | numeric literal       | The logical width of the field.
|input          | boolean literal                    | If the field is used for conveying a value into the system.
|default        | expression returns primitive value | The default value of the field.
|====================

*Example:*

[source,jsl]
----
field Integer price <= customer.cart.price;
----

=== View link

A view link can represent a relation to an other view.

*Syntax:*

[subs="quotes"]
----
[purple]#**link**# &#x5B;[purple]##**required**##] &lt;view> &lt;name> &#x5B;[purple]##**<=**## &lt;expression>]
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>]
    &#x5B;[purple]##**width:**##&lt;width>]
    &#x5B;[purple]##**text:**##&lt;text>]
    &#x5B;[purple]##**eager:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**choices:**##&lt;choices>]
    &#x5B;[purple]##**default:**##&lt;default>]
    &#x5B;[purple]##**create:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**update:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**delete:**##([purple]##**true**##|[purple]##**false**##)][purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the field.
|icon           | string literal        | The name of the icon to show for the field.
|enabled        | boolean expression    | If the field is enabled for editing.
|hidden         | boolean expression    | If the field is shown to the user.
|width          | numeric literal       | The logical width of the field.
|eager          | boolean literal              | If the fetching strategy is eager.
|choices        | Row declaration + expression | The possible values for selection.
|default        | expression returns entity    | The default value.
|text           | string expression            | The textual representation of the link.
|create         | boolean literal              | If create is allowed on this relation.
|update         | boolean literal              | If update is allowed on this relation.
|delete         | boolean literal              | If delete is allowed on this relation.
|====================

*Example:*

[source,jsl]
----
link ProductListView productList text:"Back to shopping";
----

=== View table

A view table represents collection of rows.

*Syntax:*

[subs="quotes"]
----
[purple]#**table**# &lt;row>[purple]##**[]**## &lt;name> &#x5B;[purple]##**<=**## &lt;expression>]
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>]
    &#x5B;[purple]##**width:**##&lt;width>]
    &#x5B;[purple]##**eager:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**choices:**##&lt;choices>]
    &#x5B;[purple]##**default:**##&lt;default>]
    &#x5B;[purple]##**detail:**##&lt;detail>]
    &#x5B;[purple]##**rows:**##&lt;rows>]
    &#x5B;[purple]##**create:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**update:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**delete:**##([purple]##**true**##|[purple]##**false**##)][purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the field.
|icon           | string literal        | The name of the icon to show for the field.
|enabled        | boolean expression    | If the field is enabled for editing.
|hidden         | boolean expression    | If the field is shown to the user.
|width          | numeric literal       | The logical width of the field.
|eager          | boolean literal              | If the fetching strategy is eager.
|choices        | Row declaration + expression | The possible values for selection.
|default        | expression returns entities    | The default rows.
|detail         | view name             | The view that shows the details of a row.
|rows           | Numeric literal       | The number of rows shown in the table.
|create         | boolean literal              | If create is allowed on this relation.
|update         | boolean literal              | If update is allowed on this relation.
|delete         | boolean literal              | If delete is allowed on this relation.
|====================

*Example:*

[source,jsl]
----
table ProductRow[] products <= Product.all();
----

=== View text

*Syntax:*

[subs="quotes"]
----
[purple]#**text**# &lt;name> [purple]##**<=**## &lt;expression>
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>]
    &#x5B;[purple]##**width:**##&lt;width>][purple]##**;**##
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the field.
|icon           | string literal        | The name of the icon to show for the field.
|hidden         | boolean expression    | If the field is shown to the user.
|width          | numeric literal       | The logical width of the field.
|====================

*Example:*

[source,jsl]
----
text message <= "Thank you for your order.";
----

=== View group

*Syntax:*

[subs="quotes"]
----
([purple]#**horizontal**#|[purple]#**vertical**#) &lt;name>
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>]
    &#x5B;[purple]##**width:**##&lt;width>]
    &#x5B;[purple]##**frame:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**h-align:**##([purple]##**left**##|[purple]##**right**##|[purple]##**center**##)]
    &#x5B;[purple]##**v-align:**##([purple]##**top**##|[purple]##**bottom**##|[purple]##**center**##)]
    &#x5B;[purple]##**stretch:**##([purple]##**true**##|[purple]##**false**##)]
[purple]##**{**##
    [member] ...
[purple]#**}**#[purple]##**;**##
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the field.
|icon           | string literal        | The name of the icon to show for the field.
|enabled        | boolean expression    | If the field is enabled for editing.
|hidden         | boolean expression    | If the field is shown to the user.
|width          | numeric literal       | The logical width of the field.
|frame          | boolean literal           | If the group is surrounded by a frame.
|h-align        | `left`, `right`, `center` | The horizontal location of elements inside the group. 
|v-align        | `top`, `bottom`, `center` | The vertical location of elements inside the group.
|stretch        | boolean literal           | If the group fills the available space.
|====================

*Example:*

[source,jsl]
----
vertical head {
	field String price <= product.price.asString() + " HUF";
	field String name <= product.name;
}
----

=== View tabs

*Syntax:*

[subs="quotes"]
----
[purple]#**tabs**# &lt;name>
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>]
    &#x5B;[purple]##**width:**##&lt;width>]
    &#x5B;[purple]##**frame:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**stretch:**##([purple]##**true**##|[purple]##**false**##)]
[purple]##**{**##
    [group] ...
[purple]#**}**#[purple]##**;**##
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the field.
|icon           | string literal        | The name of the icon to show for the field.
|enabled        | boolean expression    | If the field is enabled for editing.
|hidden         | boolean expression    | If the field is shown to the user.
|width          | numeric literal       | The logical width of the field.
|frame          | boolean literal       | If the tabs are surrounded by a frame.
|stretch        | boolean literal       | If the tabs fill the available space.
|====================

=== View action

The action is a piece of code (currently Java implementation) that is executed when the action is invoked. An action may receive input as defined in its parameter.

*Syntax:*

[subs="quotes"]
----
[purple]#**action**# &#x5B;&lt;view>|&lt;union>|[purple]#**void**#] &lt;name> [purple]##**(**##&#x5B;&lt;parameter>][purple]##**)**##
    &#x5B;[purple]##**throws**## &lt;error1> &#x5B;, &lt;error2>] ...]
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>]
    &#x5B;[purple]##**width:**##&lt;width>]
    &#x5B;[purple]##**resource:**##&lt;resource>][purple]##**;**##
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the field.
|icon           | string literal        | The name of the icon to show for the field.
|enabled        | boolean expression    | If the field is enabled for execution.
|hidden         | boolean expression    | If the field is shown to the user.
|width          | numeric literal       | The logical width of the field.
|resource       | access name           | The name of an access where the returned transfer object is available.
|====================

*Example:*

[source,jsl]
----
action ThankYouView order();
----

////
=== View submit

*Syntax:*

[subs="quotes"]
----
[purple]#**submit**# &#x5B;&lt;view>|&lt;union>|[purple]#**void**#] &lt;name>
&#x5B;[purple]##**throws**## &lt;error1> &#x5B;, &lt;error2>] ...]
&#x5B;&lt;parameter name> [purple]#**:**# &lt;parameter value>] ... [purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the field.
|icon           | string literal        | The name of the icon to show for the field.
|enabled        | boolean expression    | If the field is enabled for execution.
|hidden         | boolean expression    | If the field is shown to the user.
|width          | numeric literal       | The logical width of the field.
|====================

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|resource       | access name           | The name of an access where the returned transfer object is available.
|====================
////

== Row

A row is a transfer object that also defines the visual representation of data as cells in a table.

*Syntax:*

[subs="quotes"]
----
[purple]#**row**# &lt;name> &#x5B;[purple]##**(**##&lt;entity> &lt;mapping-field>[purple]##**)**##]
&#x5B;{
    &#x5B;member] ...
}] [purple]##**;**##
----

or

[subs="quotes"]
----
[purple]#**row**# &lt;name> &#x5B;[purple]##**maps**## &lt;entity> [purple]#**as**# &lt;mapping-field>]
{
    &#x5B;member] ...
}][purple]##**;**##
----

*Example:*

[source,jsl]
----
row ProductRow(Product product) {
	column String name <= product.name;
	column String price <= product.price.asString() + " HUF";
}
----

=== Row column

// _Should we rename column to cell??_

A primitive field that can store only a single primitive value and is displayed as a row cell.

*Syntax:*

[subs="quotes"]
----
[purple]#**column**# &lt;primitive> &lt;name> &#x5B;[purple]##**<=**## &lt;expression>]
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>]
    &#x5B;[purple]##**width:**##&lt;width>][purple]##**;**##
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the column.
|icon           | string literal        | The name of the icon to show for the column.
|hidden         | boolean expression    | If the column is shown to the user.
|width          | numeric literal       | The logical width of the column.
|====================

*Example:*

[source,jsl]
----
column String price <= product.price.asString() + " HUF";
----

== Actor

An actor represents a generic user of the system and can be used to represent humans or another system.

*Syntax:*

[subs="quotes"]
----
[purple]#**actor**# &lt;name> &#x5B;[purple]##**human**##] &#x5B;[purple]##**(**##&lt;entity> &lt;mapping-field>[purple]##**)**##]
    &#x5B;[purple]##**realm:**##&lt;realm>]
    &#x5B;[purple]##**claim:**##&lt;claim>]
    &#x5B;[purple]##**identity:**##&lt;identity>]
    &#x5B;[purple]##**guard:**##&lt;guard>]
&#x5B;[purple]##**{**##
    &#x5B;member] ...
[purple]##**}**##][purple]##**;**##
----

or 

[subs="quotes"]
----
[purple]#**actor**# &#x5B;[purple]##**human**##] &lt;name> &#x5B; [purple]#**maps**# &lt;entity> [purple]#**as**# &lt;mapping-field> ]
    &#x5B;[purple]##**realm:**##&lt;realm>]
    &#x5B;[purple]##**claim:**##&lt;claim>]
    &#x5B;[purple]##**identity:**##&lt;identity>]
    &#x5B;[purple]##**guard:**##&lt;guard>]
&#x5B;[purple]##**{**##
    &#x5B;member] ...
[purple]##**}**##][purple]##**;**##
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|realm          | string literal        | The name of the realm.
|claim          | string literal        | The name of the claim.
|identity       | attribute selection expression    | The attribute of the mapped entity that serves as the identifier against the claim.
|guard          | boolean expression    | The condition to allow the external user to access the services.
|====================

*Example:*

[source,jsl]
----
actor CustomerActor(Customer customer) {
	menu CartView myCart <= customer label:"My cart";
	menu ProductListView products label:"Products";
}
----

=== Actor access

Access exposes an entity or collection of entities to the user represented by the actor.

*Syntax:*

[subs="quotes"]
----
[purple]#**access**# &lt;transfer|view|row>&#x5B;[purple]##**[]**##] &lt;name> [purple]##**<=**## &lt;expression>
    &#x5B;[purple]##**choices:**##&lt;choices>]
    &#x5B;[purple]##**create:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**update:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**delete:**##([purple]##**true**##|[purple]##**false**##)][purple]#**;**#
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|create         | boolean literal       | If create is allowed on this access.
|update         | boolean literal       | If update is allowed on this access.
|delete         | boolean literal       | If delete is allowed on this access.
|====================

=== Actor group

A group can contain only menu elements.

*Syntax:*

[subs="quotes"]
----
[purple]#**group**# &lt;name>
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>]
[purple]##**{**##
    [member] ...
[purple]#**}**#[purple]##**;**##
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the menu group.
|icon           | string literal        | The name of the icon to show for the menu group.
|enabled        | boolean expression    | If the menu group is enabled to the user.
|hidden         | boolean expression    | If the menu group is shown to the user.
|====================

=== Actor single menu

Single menu is a visually represented access for human actors that provides an entity. 

*Syntax:*

[subs="quotes"]
----
[purple]#**menu**# &lt;view> &lt;name> [purple]##**<=**## &lt;expression>
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>][purple]##**;**##
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the menu.
|icon           | string literal        | the name of the icon to show for the menu.
|enabled        | boolean expression    | If the menu is enabled to the user.
|hidden         | boolean expression    | If the menu is shown to the user.
|====================

*Example:*

[source,jsl]
----
menu CartView myCart <= customer label:"My cart";
----

=== Actor table menu

Table menu is a visually represented access for human actors that provides a collection of entities. 

[subs="quotes"]
----
[purple]#**menu**# &lt;row>[purple]##**[]**## &lt;name> [purple]##**<=**## &lt;expression>
    &#x5B;[purple]##**label:**##&lt;label>]
    &#x5B;[purple]##**icon:**##&lt;icon>]
    &#x5B;[purple]##**enabled:**##&lt;enabled>]
    &#x5B;[purple]##**hidden:**##&lt;hidden>]
    &#x5B;[purple]##**choices:**##&lt;choices>]
    &#x5B;[purple]##**rows:**##&lt;rows>]
    &#x5B;[purple]##**create:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**update:**##([purple]##**true**##|[purple]##**false**##)]
    &#x5B;[purple]##**delete:**##([purple]##**true**##|[purple]##**false**##)][purple]##**;**##
----

[options="header", cols="1,1,2"]
|====================
|Modifier name | Modifier type        | Meaning
|label          | string literal        | The label to show for the menu.
|icon           | string literal        | the name of the icon to show for the menu.
|enabled        | boolean expression    | If the menu is enabled to the user.
|hidden         | boolean expression    | If the menu is shown to the user.
|choices        | Row declaration + expression  | The possible values for selection.
|rows           | Numeric literal       | The number of rows shown in the table.
|create         | boolean literal       | If create is allowed on this table.
|update         | boolean literal       | If update is allowed on this table.
|delete         | boolean literal       | If delete is allowed on this table.
|====================


= Example model

[source,jsl]
----
model shop;

import judo::types;

type string Email min-size:0 max-size:64 regex:r"^\w+@\w+(\.\w+)+$";

enum OrderStatus {
	OPEN = 1;
	ORDERED = 2;
	DELIVERED = 3;
}

entity Address {
    field required String street;
    field required String city;
    field String country;
}

entity User {
	identifier required Email email;
}

entity abstract Customer {
    field required Address address;
	relation Order[] orders opposite:customer;
	relation Order cart <= self.orders.filter(o | o.status == OrderStatus#OPEN).any();
}

query Integer NumOfCustomers(String country) <=
		Customer.
		all().
		filter(c | c.address.country == country).
		size();

entity Person extends Customer, User {
	field required String firstName;
	field required String lastName;
	field String fullName <= self.firstName + " " + self.lastName;
}

entity Enterprise extends Customer {
	field required String name;
}

entity Order {
	field required OrderStatus status default:OrderStatus#OPEN;
	field OrderItem[] orderItems;
	relation required Customer customer opposite:orders;
	field Integer price <= self.orderItems.sum(item | item.price);
}

entity OrderItem {
	relation required Product product opposite-add:orderItems[];
	field required Integer amount;
	field String productName <= self.product.name;
	field Integer price <= self.amount * self.product.price;
}

entity Product {
	identifier required String name;
	field required Integer price;
}

view ProductListView {
	table ProductRow[] products <= Product.all();
}

row ProductRow(Product product) {
	column String name <= product.name;
	column String price <= product.price.asString() + " HUF";
}

view ProductView(Product product) {
	vertical head {
		field String price <= product.price.asString() + " HUF";
		field String name <= product.name default:"";
	}
}

view CartView maps Customer as customer {
	table OrderItemRow[] items <= customer.cart.orderItems;
	field Integer price <= customer.cart.price;

	link ProductListView productList text:"Back to shopping";
	
	// set the status of the order to OrderStatus#ORDERED
	action ThankYouView order(CouponView coupon);
}

view CouponView {
    field String code;
}

view ThankYouView {
	text message <= "Thank you for your order. Please check other products at";
	link ProductListView productList text:"Our products page";
}

row OrderItemRow(OrderItem item) {
	column String name <= item.productName;
	column Integer amount <= item.amount;
	column Integer price <= item.price;
}

actor human CustomerActor(Customer customer) {
	menu CartView myCart <= customer label:"My cart";
	menu ProductListView products label:"Products";
}
----
