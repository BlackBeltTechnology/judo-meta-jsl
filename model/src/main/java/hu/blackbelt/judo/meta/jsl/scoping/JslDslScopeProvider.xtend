/*
	 * generated by Xtext 2.26.0
 */
package hu.blackbelt.judo.meta.jsl.scoping

import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.EReference
import hu.blackbelt.judo.meta.jsl.jsldsl.EntityRelationDeclaration
import hu.blackbelt.judo.meta.jsl.jsldsl.JsldslPackage
import org.eclipse.xtext.scoping.Scopes
import hu.blackbelt.judo.meta.jsl.util.JslDslModelExtension
import com.google.inject.Inject
import org.eclipse.xtext.scoping.IScope
import hu.blackbelt.judo.meta.jsl.jsldsl.EntityRelationOpposite
import hu.blackbelt.judo.meta.jsl.jsldsl.ThrowParameter
import hu.blackbelt.judo.meta.jsl.jsldsl.CreateError
import hu.blackbelt.judo.meta.jsl.jsldsl.Feature
import hu.blackbelt.judo.meta.jsl.jsldsl.QueryParameter
import hu.blackbelt.judo.meta.jsl.jsldsl.EntityDerivedDeclaration
import hu.blackbelt.judo.meta.jsl.jsldsl.NavigationExpression
import hu.blackbelt.judo.meta.jsl.jsldsl.DefaultExpression

/**
 * This class contains custom scoping description.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#scoping
 * on how and when to use it.
 */ 
 
class JslDslScopeProvider extends AbstractJslDslScopeProvider {

	@Inject extension JslDslModelExtension

    override getScope(EObject context, EReference ref) {
    	System.out.println("JslDslLocalScopeProvider.getScope="+ context.toString + " for " + ref.toString);
		switch context {
			EntityRelationOpposite : 
				switch (ref) {
					case JsldslPackage::eINSTANCE.entityRelationOpposite_OppositeType:
						return Scopes.scopeFor((context.eContainer as EntityRelationDeclaration).getAllOppositeRelations, IScope.NULLSCOPE)			
					default: 
						return super.getScope(context, ref)
				}
			DefaultExpression :
				return context.scopeForDefaultExpressionType(super.getScope(context, ref))
			/*			 
			Feature
			    : {Feature} '.' member = EntityMemberDeclarationFeature
			    ;
			
			EntityMemberDeclarationFeature returns Feature
				: entityMemberDeclarationType = [EntityMemberDeclaration | LocalName ] ('(' parameters+=QueryParameter (',' parameters+=QueryParameter)* ')')?
				;
			*/
			Feature :
				switch (ref) {
					case JsldslPackage::eINSTANCE.feature_EntityMemberDeclarationType:
						return (context as Feature).scopeForFeatureEntityMemberDeclarationType(ref, super.getScope(context, ref))
					case JsldslPackage::eINSTANCE.queryParameter_DerivedParameterType:
						return (context as Feature).scopeForQueryParameterDerivedParameterType(super.getScope(context, ref))
					default: 
						return super.getScope(context, ref)
				}
			

			/*
			CreateError
				: errorDeclarationType=[ErrorDeclaration | LocalName] ('(' (parameters+=ThrowParameter (',' parameters+=ThrowParameter)*)? ')')?
				;
			
			ThrowParameter
				: errorFieldType=[ErrorField | LocalName] '=' expession=Expression
				;
			*/
			CreateError : 
				switch (ref) {
					case JsldslPackage::eINSTANCE.throwParameter_ErrorFieldType:
						return (context as CreateError).scopeForCreateError
					default: 
						return super.getScope(context, ref)
				}

			ThrowParameter : 
				switch (ref) {
					case JsldslPackage::eINSTANCE.throwParameter_ErrorFieldType:
						if (context.eContainer instanceof CreateError) {
							return (context.eContainer as CreateError).scopeForCreateError
						} else {
							return super.getScope(context, ref)
						}
					default: 
						return super.getScope(context, ref)
				}

			/*
			Feature
				: {Feature} '.' name=ID ('(' parameters+=QueryParameter (',' parameters+=QueryParameter)* ')')?
				;
			
			QueryParameter
				:  derivedParameterType=[DerivedParameter | LocalName] '=' expression=MultilineExpression
				;
			 */
			QueryParameter : 
				switch (ref) {
					case JsldslPackage::eINSTANCE.queryParameter_DerivedParameterType:
						return (context.eContainer as Feature).scopeForQueryParameterDerivedParameterType(super.getScope(context, ref))
					case JsldslPackage::eINSTANCE.queryParameter_Parameter:
						return (context.eContainer as Feature).scopeForQueryParameterParameterType
					default: 
						return super.getScope(context, ref)
				}
				
				
			/*
			NavigationBase returns NavigationExpression
				: {NavigationExpression} qName=LocalName (features+=Feature* | '#' enumValue = [ EnumLiteral | ID ])
				;
			 */
			NavigationExpression:
				switch (ref) {
					case JsldslPackage::eINSTANCE.navigationExpression_EnumValue: {
//						val Feature feature = (context as NavigationExpression).features.last
						//feature.modelDeclaration.allEnumDeclarations.findFirst[f | f.name == q]
						val feature = context.eContainer
						val decl = feature.modelDeclaration.allEnumDeclarations
            			val enumDeclaration = decl.findFirst[e | e.name.equals((context as NavigationExpression).QName)];
						if (enumDeclaration !== null) {
							return Scopes.scopeFor(enumDeclaration.literals, IScope.NULLSCOPE)
						} else {
							return IScope.NULLSCOPE									
						}
					}	
						
					default: 
						return super.getScope(context, ref)
				}
			default: 
				return super.getScope(context, ref)
		}		
	}
	
	def IScope scopeForCreateError(CreateError createError) {
		return Scopes.scopeFor(createError.errorDeclarationType.fields, IScope.NULLSCOPE)
	}

	def IScope scopeForQueryParameterDerivedParameterType(Feature feature, IScope fallback) {
		if (feature.entityMemberDeclarationType !== null 
			&& feature.entityMemberDeclarationType instanceof EntityDerivedDeclaration) {
			return Scopes.scopeFor((feature.entityMemberDeclarationType as EntityDerivedDeclaration).parameters, IScope.NULLSCOPE)							
		} else {
			return fallback
		}
	}

	def IScope scopeForQueryParameterParameterType(Feature feature) {
		return Scopes.scopeFor(feature.getDerivedDeclaration.parameters, IScope.NULLSCOPE)							
	}
	
	def IScope scopeForDefaultExpressionType(DefaultExpression expression, IScope fallback) {
		if (expression.defaultExpression instanceof NavigationExpression) {
			val decl = expression.modelDeclaration.allEnumDeclarations
            val enumDeclaration = decl.findFirst[e | e.name.equals((expression.defaultExpression as NavigationExpression).QName)];

            if (enumDeclaration !== null) {
                return Scopes.scopeFor(enumDeclaration.literals, fallback)
            } else {
                return fallback
            }
		}
		return fallback
	}

	def IScope scopeForFeatureEntityMemberDeclarationType(Feature feature, EReference ref, IScope fallback) {
		System.out.println("JslDslLocalScopeProvider.scopeForFeatureEntityMemberDeclarationType="+ feature.toString)
		if (feature.eContainer instanceof NavigationExpression) {
            // enums...
            val decl = feature.modelDeclaration.allEnumDeclarations
            val enumDeclaration = decl.findFirst[e | e.name.equals((feature.eContainer as NavigationExpression).QName)];

            if (enumDeclaration !== null) {
                return Scopes.scopeFor(enumDeclaration.literals, fallback)
            } else {
                return Scopes.scopeFor(feature.modelDeclaration.allEntityMemberDeclarations, fallback)
            }
        } else {
            return feature.getScopeForFeature(ref, fallback)
        }
	}
}
