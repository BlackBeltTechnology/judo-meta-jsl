module hu.blackbelt.judo.meta.jslinternal.GenerateJslDsl

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var modelDir = "model/model"
var javaGenPath = "model/src/main/xtext-gen"

Workflow {
	
	    // Generate UI content assistant and validators
		component = XtextGenerator {
		configuration = {
			project = JslXtextProjectConfig {
			    mavenLayout = true
				baseName = "model"
				rootPath = rootPath
				runtime = {
					overwriteSrc = false
					enabled = false
				}
				runtimeTest = {
					overwriteSrc = false
					enabled = false
				}
								
				genericIde = {
					overwriteSrc = false
					enabled = false
				}
				
				eclipsePlugin = {
					overwriteSrc = false
					enabled = true
					name = "hu.blackbelt.judo.meta.jsl.ide.ui"
					root = "${rootPath}/ide/ui"
				}
				
				eclipsePluginTest = {
					overwriteSrc = false
					enabled = false
				}
				web = {
					overwriteSrc = false
					enabled = false
				}
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		// Customization of generator, see org.eclipse.xtext.xtext.generator.StandardLanguage class for standard elements
		language = XtextGeneratorLanguage {
			name = "hu.blackbelt.judo.meta.jsl.JslDsl"
			fileExtensions = "jsl"

            fragment = grammarAccess.GrammarAccessFragment2 {}
            fragment = ecore.EMFGeneratorFragment2 {}
            fragment = serializer.SerializerFragment2 {
                generateStub = false
            }
            fragment = resourceFactory.ResourceFactoryFragment2 {}
            fragment = parser.antlr.XtextAntlrGeneratorFragment2 {
                options = { 
                    ignoreCase = true
                }
            }
            fragment = validation.ValidatorFragment2 {
				generatePropertyPage = true
            }
            fragment = builder.BuilderIntegrationFragment2 {}
            fragment = generator.GeneratorFragment2 {}
            fragment = ui.contentAssist.ContentAssistFragment2 {}			           		
		}
	}	
	
	// Generate runtime
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
			    mavenLayout = true
				baseName = "model"
				rootPath = rootPath
				runtime = {
					enabled = true
				}
				runtimeTest = {
					enabled = false
				}
				eclipsePlugin = {
					enabled = false
				}
				eclipsePluginTest = {
					enabled = false
				}
				web = {
					enabled = false
				}
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			
			name = "hu.blackbelt.judo.meta.jsl.JslDsl"
			fileExtensions = "jsl"

            parserGenerator = {
                options = {
                    ignoreCase = true
                }
                debugGrammar = true
            }
            
            scopeProvider = {
                ignoreCase = true
            }

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
	
	
	// Helper for model
	component = hu.blackbelt.eclipse.emf.genmodel.generator.helper.HelperGeneratorWorkflow {
		modelDir = "${rootPath}/${modelDir}"
		javaGenPath = "${rootPath}/${javaGenPath}"
	}

	// Builder for model
	component = hu.blackbelt.eclipse.emf.genmodel.generator.builder.BuilderGeneratorWorkflow {
		modelDir = "${rootPath}/${modelDir}"
		javaGenPath = "${rootPath}/${javaGenPath}"
	}	

	// Judo RuntimeModel for model
	component = hu.blackbelt.judo.eclipse.emf.genmodel.generator.runtimemodel.RuntimeModelGeneratorWorkflow {
		modelDir = "${rootPath}/${modelDir}"
		javaGenPath = "${rootPath}/${javaGenPath}"
	}
	

}
