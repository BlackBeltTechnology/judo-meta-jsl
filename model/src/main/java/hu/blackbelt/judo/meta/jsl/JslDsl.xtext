grammar hu.blackbelt.judo.meta.jsl.JslDsl hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate jsldsl "http://www.blackbelt.hu/judo/meta/jsl/JslDsl"
 
ModelDeclaration returns ModelDeclaration
    : {ModelDeclaration}
      annotations += AnnotationMark*
      KW_MODEL name = ModelName SC
      imports += ModelImportDeclaration*
	  (declarations += Declaration)*
    ;

ModelImportDeclaration 
    : KW_IMPORT model = [ModelDeclaration | ModelName] (KW_AS alias = JSLID)? SC
	;

Declaration
	: TypeDeclaration
	| ErrorDeclaration
	| QueryDeclaration
	| FunctionDeclaration
	| LambdaDeclaration
	| AnnotationDeclaration
	;

SingleType 
	: PrimitiveDeclaration
	| EntityDeclaration
	;

TypeDeclaration
	: PrimitiveDeclaration
	| ClassDeclaration
	;

PrimitiveDeclaration
	: EnumDeclaration
	| DataTypeDeclaration
    ;

ClassDeclaration
	: EntityDeclaration
	| TransferDeclaration
	| ViewDeclaration
	| RowDeclaration
	| ActorDeclaration
	;

fragment Named
	: => name = JSLID;

fragment Cardinality
    : many ?= '[]'
    ;

/****************************
 *	Entity
 ****************************/

EntityDeclaration
	: {EntityDeclaration}
	  annotations += AnnotationMark*
	  KW_ENTITY abstract ?= KW_ABSTRACT? Named
	  (KW_EXTENDS extends += [EntityDeclaration | ModelName] (',' extends += [EntityDeclaration | ModelName])*)?
	  (SC | BLOCK_START (members += EntityMemberDeclaration)* BLOCK_END)
	;

EntityMemberDeclaration
	: {EntityStoredFieldDeclaration}
	  annotations += AnnotationMark*
	  ( FEAT_FIELD required ?= ATTR_REQUIRED? singleReferenceType = [SingleType | ModelName]
	    Named ('=' defaultExpr = Expression)?
	  | FEAT_FIELD entityReferenceType = [EntityDeclaration | ModelName] Cardinality
	  	Named ('=' defaultExpr = Expression)?
	  | identifier ?= FEAT_IDENTIFIER required ?= ATTR_REQUIRED? primitiveReferenceType = [PrimitiveDeclaration | ModelName]
	    Named ('=' defaultExpr = Expression)?
	  )
	  SC
	  | EntityStoredRelationDeclaration
	  | EntityCalculatedMemberDeclaration
	;

EntityStoredRelationDeclaration
	: {EntityStoredRelationDeclaration}
	  annotations += AnnotationMark*
	  ( FEAT_RELATION required ?= ATTR_REQUIRED? entityReferenceType = [EntityDeclaration | ModelName]
	  	Named ('=' defaultExpr = Expression)? opposite = EntityRelationOpposite?
	  | FEAT_RELATION entityReferenceType = [EntityDeclaration | ModelName] Cardinality
	  	Named ('=' defaultExpr = Expression)? opposite = EntityRelationOpposite?
	  )
	  SC
	;

EntityCalculatedMemberDeclaration
	: {EntityCalculatedFieldDeclaration}
	  annotations += AnnotationMark*
	  FEAT_FIELD primitiveReferenceType = [PrimitiveDeclaration | ModelName]
	  Named
	  ('(' parameters += QueryParameterDeclaration (',' parameters += QueryParameterDeclaration)* ')')? '<=' getterExpr = Expression
	  SC
	  | EntityCalculatedRelationDeclaration
	;

EntityCalculatedRelationDeclaration
	: {EntityCalculatedRelationDeclaration}
	  annotations += AnnotationMark*
	  FEAT_RELATION entityReferenceType = [EntityDeclaration | ModelName] Cardinality?
	  Named
	  ('(' parameters += QueryParameterDeclaration (',' parameters += QueryParameterDeclaration)* ')')? '<=' getterExpr = Expression
	  SC
	;

EntityRelationOpposite
	: EntityRelationOppositeInjected
	| EntityRelationOppositeReferenced
	;

EntityRelationOppositeInjected
	: ATTR_OPPOSITEADD ':' Named Cardinality?
	;

EntityRelationOppositeReferenced
	: ATTR_OPPOSITE ':' oppositeType = [EntityStoredRelationDeclaration | LocalName]
	;

//EntityOperationDeclaration
//	: annotations += AnnotationMark*
//	  'operation' static ?= 'static'? (CONST_VOID? | return = EntityOperationReturnDeclaration) Named
//	  ('(' (parameters += EntityOperationParameterDeclaration (',' parameters += EntityOperationParameterDeclaration)*)? ')')
//	  (SC | BLOCK_START BLOCK_END )
//	;
//
//EntityOperationReturnDeclaration
//	: referenceType = [SingleType | ModelName] Cardinality?
//	;
//
//EntityOperationParameterDeclaration
//	: referenceType = [SingleType | ModelName] Cardinality? Named
//	;

EntityMapDeclaration
	: '(' entity = [EntityDeclaration | ModelName] Named ')'  // if no name defined, the 'self' shall be default name
	| KW_MAPS entity = [EntityDeclaration | ModelName] KW_AS Named
	;


/****************************
 *	Query
 ****************************/

QueryDeclaration
	: KW_QUERY (referenceType = [SingleType | ModelName] Cardinality?) Named
	  ('(' (parameters += QueryParameterDeclaration (',' parameters += QueryParameterDeclaration)*)? ')')
	  "<=" expression = Expression
	  SC
	;

QueryParameterDeclaration
	: referenceType = [PrimitiveDeclaration | ModelName] Named ("=" default = Expression)?
	;

/****************************
 *	Return
 ****************************/

ReturnDeclaration
	: {ReturnDeclaration} CONST_VOID?
	| referenceTypes += [TransferDeclaration | ModelName]
	| '<' referenceTypes += [TransferDeclaration | ModelName] ("|" referenceType += [TransferDeclaration | ModelName])+ '>'
	;

/****************************
 *	Transfer
 ****************************/

//Transferable
//	: TransferDeclaration
//	| PrimitiveDeclaration
//	;

TransferDeclaration
	: {TransferDeclaration}
	  annotations += AnnotationMark*
	  KW_TRANSFER Named
	  map = EntityMapDeclaration?
	  (automap ?= SC | BLOCK_START members += TransferMemberDeclaration* BLOCK_END)
	;

TransferMemberDeclaration
	: TransferConstructorDeclaration
	| TransferDestructorDeclaration
	| TransferInitializerDeclaration
	
	| TransferDataMemberDeclaration

	| TransferActionDeclaration
	| TransferSubmitDeclaration
	;

TransferDataMemberDeclaration
	: TransferFieldDeclaration
	| TransferRelationDeclaration
	;

TransferDestructorDeclaration
	: {TransferGetDeclaration}
	  FEAT_DESTRUCTOR
	  (SC | BLOCK_START BLOCK_END)
	;

TransferConstructorDeclaration
	: {TransferConstructorDeclaration}
	  FEAT_CONSTRUCTOR
	  (SC | BLOCK_START (defaults += TransferDefault)* BLOCK_END)
	;

TransferInitializerDeclaration
	: {TransferInitializerDeclaration}
	  FEAT_INITIALIZER
	  (SC | BLOCK_START BLOCK_END)
	;

TransferDefault
	: member = TransferDataMember '=' rightValue = Expression SC
	;

TransferDataMember
	: 'self' '.' reference = [TransferDataMemberDeclaration | ModelName]
	;

TransferFieldDeclaration
	: annotations += AnnotationMark*
	  FEAT_FIELD  required ?= ATTR_REQUIRED? referenceType = [PrimitiveDeclaration | ModelName] Named
	  ((maps ?= '<=>' | reads ?= '<=') expression = Expression)?
	  SC
	;

TransferRelationDeclaration
	: annotations += AnnotationMark*
	  FEAT_RELATION referenceType = [TransferDeclaration | ModelName] Cardinality? Named ((maps ?= '<=>' | reads ?= '<=') expression = Expression)?
	  (choices = TransferChoiceModifier?)
	  SC
	;

TransferSubmitDeclaration
	: annotations += AnnotationMark*
	  FEAT_SUBMIT return = ReturnDeclaration Named
	  SC
	;

TransferActionDeclaration
	: annotations += AnnotationMark*
	  FEAT_ACTION return = ReturnDeclaration Named
	  '(' (parameterType = [TransferDeclaration | ModelName] paramaterName = JSLID)? ')'
	  SC
	;

/****************************
 *	View
 ****************************/

ViewDeclaration returns TransferDeclaration
	: {ViewDeclaration}
	  annotations += AnnotationMark*
	  KW_VIEW Named
	  map = EntityMapDeclaration?
	  (automap ?= SC | BLOCK_START members += ViewMemberDeclaration* BLOCK_END)
	;

ViewMemberDeclaration returns TransferMemberDeclaration
	: TransferConstructorDeclaration
	| TransferDestructorDeclaration
	| TransferInitializerDeclaration

	| ViewTextDeclaration
	| ViewFieldDeclaration
	| ViewLinkDeclaration
	| ViewTableDeclaration
	
	| ViewSubmitDeclaration
	| ViewActionDeclaration

	| ViewGroupDeclaration
	| ViewTabsDeclaration
	;

//ViewDefault
//	: field = ViewField '=' rightValue = Expression SC
//	;
//
//ViewField
//	: 'self' '.' declaration = [ViewFieldDeclaration | ModelName]
//	;

ViewTabsDeclaration
	: {ViewTabsDeclaration}
	  annotations += AnnotationMark*
	  FEAT_TABS tag = JSLID
	  ( label = LabelModifier?
	  & icon = IconModifier?
	  & enabled = EnabledModifier?
	  & ^hidden = HiddenModifier?
	  & width = WidthModifier?
	  & frame = FrameModifier?)
	  BLOCK_START (members += ViewGroupDeclaration)* BLOCK_END
	;

ViewGroupDeclaration
	: {ViewGroupDeclaration} 
	  annotations += AnnotationMark*
	  FEAT_GROUP tag = JSLID
	  ( (horizontal = HorizontalModifier | vertical = VerticalModifier)?
	  & label = LabelModifier?
	  & icon = IconModifier?
	  & enabled = EnabledModifier?
	  & ^hidden = HiddenModifier?
	  & width = WidthModifier?
	  & frame = FrameModifier?)
	  BLOCK_START (members += ViewMemberDeclaration)* BLOCK_END
	;

HorizontalModifier
	: ATTR_HORIZONTAL ':' (left ?= CONST_LEFT | right ?= CONST_RIGHT | center ?= CONST_CENTER)
	;

VerticalModifier
	: ATTR_VERTICAL ':' (top ?= CONST_TOP | bottom ?= CONST_BOTTOM | center ?= CONST_CENTER | stretch ?= CONST_STRETCH)
	;

ViewTextDeclaration returns TransferFieldDeclaration
	: {ViewTextDeclaration}
	  annotations += AnnotationMark*
	  FEAT_TEXT Named reads ?= '<=' expression = Expression
	  ( label = LabelModifier?
	  & icon = IconModifier?
	  & ^hidden = HiddenModifier?
	  & width = WidthModifier?)
	  SC
	;

ViewFieldDeclaration returns TransferFieldDeclaration
	: {ViewFieldDeclaration}
	  annotations += AnnotationMark*
	  FEAT_FIELD  required ?= ATTR_REQUIRED? referenceType = [PrimitiveDeclaration | ModelName] Named
	  ((maps ?= '<=>' | reads ?= '<=') getterExpr = Expression)?
	  ( label = LabelModifier?
	  & icon = IconModifier?
	  & enabled = EnabledModifier?
	  & ^hidden = HiddenModifier?
	  & width = WidthModifier?)
	  SC
	;

//TransferFieldDeclaration
//	: annotations += AnnotationMark*
//	  FEAT_FIELD  required ?= ATTR_REQUIRED? referenceType = [PrimitiveDeclaration | ModelName] Named
//	  ((maps ?= '<=>' | reads ?= '<=') expression = Expression)?
//	  SC
//	;

ViewLinkDeclaration returns TransferRelationDeclaration
	: {ViewLinkDeclaration}
	  annotations += AnnotationMark*
	  FEAT_LINK required ?= ATTR_REQUIRED? referenceType = [ViewDeclaration | ModelName] Named
	  ((maps ?= '<=>' | reads ?= '<=') getterExpr = Expression)?
	  ( choices = ViewChoiceModifier?
	  & create = CreateActionModifier?
	  & text = TextModifier?
	  & label = LabelModifier?
	  & icon = IconModifier?
	  & enabled = EnabledModifier?
	  & ^hidden = HiddenModifier?
	  & width = WidthModifier?)
	  SC
	;

ViewTableDeclaration returns TransferRelationDeclaration
	: {ViewTableDeclaration}
	  annotations += AnnotationMark*
	  FEAT_TABLE referenceType = [RowDeclaration | ModelName] Cardinality Named
	  ((maps ?= '<=>' | reads ?= '<=') getterExpr = Expression)?
	  ( choices = ViewChoiceModifier?
	  & create = CreateActionModifier?
	  & rows = RowsModifier?
	  & label = LabelModifier?
	  & icon = IconModifier?
	  & enabled = EnabledModifier?
	  & ^hidden = HiddenModifier?
	  & width = WidthModifier?)
	  SC
	;

ViewSubmitDeclaration returns TransferSubmitDeclaration
	: {ViewSubmitDeclaration}
	  annotations += AnnotationMark*
	  FEAT_SUBMIT return = ReturnDeclaration Named
	  ( label = LabelModifier?
	  & icon = IconModifier?
	  & enabled = EnabledModifier?
	  & ^hidden = HiddenModifier?
	  & width = WidthModifier?
	  & resource = ResourceModifier?)
	  SC
	;

ViewActionDeclaration returns TransferActionDeclaration
	: {ViewActionDeclaration}
	  annotations += AnnotationMark*
	  FEAT_ACTION return = ReturnDeclaration Named
	  '(' (parameterType = [ViewDeclaration | ModelName] paramaterName = JSLID)? ')'
	  ( label = LabelModifier?
	  & icon = IconModifier?
	  & enabled = EnabledModifier?
	  & ^hidden = HiddenModifier?
	  & width = WidthModifier?
	  & resource = ResourceModifier?)
	  SC
	;

ResourceModifier
	: FEAT_RESOURCE ':' value = [ActorAccessDeclaration | ModelName]
	;

LabelModifier
	: ATTR_LABEL ':' value = StringLiteral
	;

IconModifier
	:  ATTR_ICON ':' value = StringLiteral
	;	

FrameModifier
	: ATTR_FRAME ':' expression = Expression
	;	
	
EnabledModifier
	: ATTR_ENABLED ':' expression = Expression
	;

HiddenModifier
	: ATTR_HIDDEN ':' expression = Expression
	;

WidthModifier
	: ATTR_WIDTH ':' value = Natural
	;

RowsModifier
	: ATTR_ROWS ':' value = Natural
	;

TransferChoiceModifier
	: ATTR_CHOICES ':' expression = Expression
	;

ViewChoiceModifier returns TransferChoiceModifier
	: ATTR_CHOICES ':' rows = [RowDeclaration | ModelName] '[]' '(' expression = Expression ')'
	;


TextModifier
	: FEAT_TEXT ':' expression = Expression
	;

CreateActionModifier
	: ATTR_CREATE ':' view = [ViewDeclaration | ModelName]
	;

DetailModifier
	: ATTR_DETAIL ':' view = [ViewDeclaration | ModelName]
	;

//TargetTransferModifier
//	: ATTR_TARGET ':' transfer = [TransferDeclaration | ModelName] ('(' expression = Expression ')')?
//	;

//ServiceFunctionCall
//	: declaration = [ServiceFunctionDeclaration | ModelName] '(' argument = Expression? ')'
//	;

/****************************
 *	Row
 ****************************/

RowDeclaration returns TransferDeclaration
	: {RowDeclaration}
	  annotations += AnnotationMark*
	  KW_ROW Named
	  map = EntityMapDeclaration?
	  detail = DetailModifier?
	  (automap ?= SC | BLOCK_START (members += RowMemberDeclaration)* BLOCK_END)
	;

RowMemberDeclaration returns TransferMemberDeclaration
	: RowColumnDeclaration
	| ViewActionDeclaration
	;

RowColumnDeclaration returns TransferFieldDeclaration
	: {RowColumnDeclaration}
	  annotations += AnnotationMark*
	  FEAT_COLUMN referenceType = [PrimitiveDeclaration | ModelName] Named ((maps ?= '<=>' | reads ?= '<=') expression = Expression)?
	  ( label = LabelModifier?
	  & icon = IconModifier?
	  & width = WidthModifier?)
	  SC
	;

/****************************
 *	Actor
 ****************************/

ActorDeclaration
	: {ActorDeclaration}
	  annotations += AnnotationMark*
	  KW_ACTOR (human ?= KW_HUMAN | system ?= KW_SYSTEM) Named
	  map = EntityMapDeclaration?
	  ( realm = RealmModifier?
	  & claim = ClaimModifier?
	  & identity = IdentityModifier?
	  & guard = GuardModifier?)
	  (SC | BLOCK_START (members += ActorMemberDeclaration)* BLOCK_END)
	;

RealmModifier
	: ATTR_REALM ':' value = StringLiteral
	;

ClaimModifier
	: ATTR_CLAIM ':' value = StringLiteral
	;

IdentityModifier
	: ATTR_IDENTITY ':' expression = Expression
	;

GuardModifier
	: ATTR_GUARD ':' expression = Expression
	;

ActorMemberDeclaration
	: ActorResourceDeclaration
	| ActorMenuDeclaration
//	| ActorAccessViewDeclaration
//	| ActorAccessTableDeclaration
//	| ActorMenuDeclaration
	| ActorGroupDeclaration
	;

ActorGroupDeclaration
	: annotations += AnnotationMark*
	  FEAT_GROUP tag = JSLID
	  ( label = LabelModifier?
	  & icon = IconModifier?
	  & enabled = EnabledModifier?
	  & ^hidden = HiddenModifier?)
	  BLOCK_START (members += ActorMenuDeclaration)* BLOCK_END
	;

//ActorAccessViewDeclaration
//	: annotations += AnnotationMark*
//	  (access ?= FEAT_ACCESS | menu ?= FEAT_MENU) referenceType = [ViewDeclaration | ModelName] Named
//	  '<=' expression = Expression
//	  ( guard = GuardModifier?
//	  & label = LabelModifier?
//	  & icon = IconModifier?
//	  & enabled = EnabledModifier?
//	  & ^hidden = HiddenModifier?)
//	  SC
//	;

ActorAccessDeclaration
	: ActorResourceDeclaration
	| ActorMenuDeclaration
	;

ActorResourceDeclaration
	: annotations += AnnotationMark*
	  FEAT_RESOURCE referenceType = [TransferDeclaration | ModelName] Cardinality? Named
	  '<=' expression = Expression
//	  guard = GuardModifier?
	  SC
	;

ActorMenuDeclaration
	: annotations += AnnotationMark*
	  FEAT_MENU referenceType = [TransferDeclaration | ModelName] Cardinality? Named
	  ('<=' expression = Expression)?
	  ( choices = ViewChoiceModifier?
//	  & guard = GuardModifier?
	  & create = CreateActionModifier?
	  & label = LabelModifier?
	  & icon = IconModifier?
	  & enabled = EnabledModifier?
	  & ^hidden = HiddenModifier?
	  & rows = RowsModifier?)
	  SC
	;

//ActorMenuViewDeclaration
//	: annotations += AnnotationMark*
//	  FEAT_MENU referenceType = [ViewDeclaration | ModelName] Named
//	  '<=' expression = Expression
//	  ( target = TargetViewModifier
//	  & guard = GuardModifier?
//	  & label = LabelModifier?
//	  & icon = IconModifier?
//	  & enabled = EnabledModifier?
//	  & ^hidden = HiddenModifier?)
//	  SC
//	;
//
//ActorAccessDeclaration
//	: annotations += AnnotationMark*
//	  FEAT_ACCESS Named
//	  ( target = TargetTransferModifier
//	  	guard = GuardModifier?)
//	  SC
//	;

/****************************
 *	Datatype
 ****************************/

DataTypeDeclaration
    : annotations += AnnotationMark*
      KW_TYPE
      ( primitive = CONST_BOOLEAN Named
      | primitive = CONST_BINARY Named (mimeTypes = ModifierMimeTypes & maxFileSize = ModifierMaxFileSize)
      | primitive = CONST_STRING Named (minSize = ModifierMinSize & maxSize = ModifierMaxSize & regex = ModifierRegex?)
      | primitive = CONST_NUMERIC Named (precision = ModifierPrecision & scale = ModifierScale)
      | primitive = CONST_DATE Named
      | primitive = CONST_TIME Named
      | primitive = CONST_TIMESTAMP Named
      )
      SC
    ;


/****************************
 *	Enum
 ****************************/

EnumDeclaration
    : annotations += AnnotationMark*
      KW_ENUM Named
      BLOCK_START (literals += EnumLiteral)* BLOCK_END
    ;

EnumLiteral
    : annotations += AnnotationMark*
      Named '=' value = Natural SC
    ;


ModifierMinSize
	: ATTR_MINSIZE ':' value = Natural
	;

ModifierMaxSize
	: ATTR_MAXSIZE ':' value = Natural
	;

ModifierRegex
	: ATTR_REGEX ':' regex = StringLiteral
	;

ModifierPrecision
	: ATTR_PRECISION ':' value = Natural
	;

ModifierScale
	: ATTR_SCALE ':' value = Natural
	;

ModifierMimeTypes
	: ATTR_MIMETYPE ':' '[' values += MimeType (',' values += MimeType)* ']'
	;

MimeType
	: value = StringLiteral
	;

ModifierMaxFileSize
	: ATTR_MAXFILESIZE ':' numeric = Natural (measure = (OTHER_KB | OTHER_MB | OTHER_GB | OTHER_KIB | OTHER_MIB | OTHER_GIB))? 
	;
	
//DataUnit
//	: KB ?= OTHER_KB
//	| MB ?= OTHER_MB
//	| GB ?= OTHER_GB
//	| KiB ?= OTHER_KIB
//	| MiB ?= OTHER_MIB
//	| GiB ?= OTHER_GIB
//	;

/****************************
 *	Expression
 ***************************/

Expression returns Expression:
    SwitchExpression;

SwitchExpression returns Expression
	: ImpliesExpression (=> ({TernaryOperation.condition=current} '?')
	  thenExpression=SwitchExpression ':'
      elseExpression=SwitchExpression)?
    ;

ImpliesExpression returns Expression
	: OrExpression (=> ({BinaryOperation.leftOperand=current} operator=OTHER_IMPLIES) rightOperand=OrExpression)*
	;

OrExpression returns Expression
	: XorExpression (=> ({BinaryOperation.leftOperand=current} operator=OTHER_OR) rightOperand=XorExpression)*
	;

XorExpression returns Expression
	: AndExpression (=> ({BinaryOperation.leftOperand=current} operator=OTHER_XOR) rightOperand=AndExpression)*
	;

AndExpression returns Expression
	: EqualityExpression (=> ({BinaryOperation.leftOperand=current} operator=OTHER_AND) rightOperand=EqualityExpression)*
	;

EqualityExpression returns Expression
	: RelationalExpression (=> ({BinaryOperation.leftOperand=current} operator=('!='|'==')) rightOperand=RelationalExpression)*
	;

RelationalExpression returns Expression
	: AdditiveExpression (=> ({BinaryOperation.leftOperand=current} operator=('>=' | '<=' | '>' | '<')) rightOperand=AdditiveExpression)*
	;

AdditiveExpression returns Expression
	: MultiplicativeExpression (=> ({BinaryOperation.leftOperand=current} operator=('+'|'-')) rightOperand=MultiplicativeExpression)*
	;

MultiplicativeExpression returns Expression
	: ExponentExpression (=> ({BinaryOperation.leftOperand=current} operator=('*' | '/' | OTHER_DIV | OTHER_MOD)) rightOperand=ExponentExpression)*
	;

ExponentExpression returns Expression
	: UnaryOperation (=> ({BinaryOperation.leftOperand=current} operator='^') rightOperand=UnaryOperation)*
	;
 
UnaryOperation returns Expression
	: {UnaryOperation} operator='not' operand=UnaryOperation
	| Navigation
    ;

Navigation returns Expression
	: {Navigation} base = NavigationBase (features += Feature)*
	;

NavigationBase
	: Self
	| Parentheses
	| NavigationBaseDeclarationReference
	| QueryCall
	| Literal
	;

Self
	: isSelf ?= 'self'
	;

Parentheses
	: '(' => expression = Expression ')'		
	;
 
NavigationBaseDeclarationReference
	: {NavigationBaseDeclarationReference} reference = [NavigationBaseDeclaration | ModelName]
 	;
 
NavigationBaseDeclaration
	: EntityDeclaration
	| LambdaVariable
	| QueryParameterDeclaration
	| PrimitiveDeclaration
	| EntityMapDeclaration
	;

QueryCall
	: declaration = [QueryDeclaration | ModelName ] '(' (arguments += QueryArgument (',' arguments += QueryArgument)*)? ')'
	;

Feature
	: MemberReference
	| Call
	;

MemberReference
    : '.' member = [NavigationTarget | LocalName] ('(' (arguments += QueryArgument (',' arguments += QueryArgument)*)? ')')?
    ;

Call
//	: EntityQueryCall
	: FunctionCall
	| LambdaCall
	;

//EntityQueryCall
//	: '.' declaration = [EntityDerivedDeclaration | LocalName ] => '(' (arguments += QueryArgument (',' arguments += QueryArgument)*)? ')'
//	;

FunctionCall
	: '!' declaration = [FunctionDeclaration | LocalName ] '(' (arguments += FunctionArgument (',' arguments += FunctionArgument)*)? ')'
	;

FunctionArgument
    : declaration = [FunctionParameterDeclaration | LocalName ] '=' expression = Expression
    ;

QueryArgument
    : declaration = [QueryParameterDeclaration | LocalName ] '=' expression = Expression
    ;

LambdaCall
	: '!' declaration = [LambdaDeclaration | LocalName ] '(' variable = LambdaVariable => '|' lambdaExpression = Expression ')';

LambdaVariable
    : {LambdaVariable} Named    	
    ;


/****************************
 *	Function
 ***************************/

FunctionDeclaration
	: FEAT_FUNCTION returnType = TypeDescription
	  Named '(' (parameters += FunctionParameterDeclaration (',' parameters += FunctionParameterDeclaration)*)? ')'
	  KW_ON baseType = TypeDescription
	  SC
	;

FunctionParameterDeclaration
	: isRequired ?= ATTR_REQUIRED? description = TypeDescription Named
	;

TypeDescription
	: {TypeDescription} type = Type
	| collection ?= OTHER_COLLECTION '<' type = Type '>'
	| declaration ?= OTHER_DECLARATION '<' type = Type '>'
	| constant ?= OTHER_CONSTANT '<' type = Type '>'
	;

Type
	: CONST_BOOLEAN | CONST_BINARY | CONST_STRING | CONST_NUMERIC | CONST_DATE | CONST_TIME | CONST_TIMESTAMP | KW_ENUM | KW_ENTITY
	;

LambdaDeclaration 
	: OTHER_LAMBDA returnType = TypeDescription
	  Named '(' (expressionType = TypeDescription)? ')'
	  SC
	;


/****************************
 *	Annotation
 ***************************/

AnnotationDeclaration
	: KW_ANNOTATION Named ('(' (parameters += AnnotationParameterDeclaration (',' parameters += AnnotationParameterDeclaration)*)? ')')?
	  (KW_ON targets += AnnotationTarget (',' targets += AnnotationTarget)*)?
	  (SC | BLOCK_START annotations += AnnotationMark* BLOCK_END)
	;

AnnotationParameterDeclaration
	: referenceType = AnnotationParameterType Named
	;

AnnotationParameterType
	: type = (CONST_BOOLEAN	| CONST_STRING | CONST_NUMERIC)
	;

AnnotationTarget
	: model ?= KW_MODEL
	| ^import ?= KW_IMPORT
	| type ?= KW_TYPE
	| enumeration ?= KW_ENUM
	| KW_ENUM '::' enumLiteral ?= FEAT_LITERAL
	| entity ?= KW_ENTITY
	| KW_ENTITY '::' entityField ?= FEAT_FIELD
//	| KW_ENTITY '::' entityIdentifier ?= FEAT_IDENTIFIER
//	| KW_ENTITY '::' entityQuery ?= FEAT_QUERY
	| KW_ENTITY '::' entityRelation ?= FEAT_RELATION
//	| KW_ENTITY '::' entityDerived ?= FEAT_DERIVED
//	| KW_ENTITY '::' entityOperation ?= 'operation'
	| transfer ?= KW_TRANSFER
	| KW_TRANSFER '::' transferField ?= FEAT_FIELD
	| KW_TRANSFER '::' transferConstructor ?= FEAT_CONSTRUCTOR
	| KW_TRANSFER '::' transferRelation ?= FEAT_RELATION
	| KW_TRANSFER '::' transferFunction ?= FEAT_FUNCTION
//	| service ?= KW_SERVICE
//	| KW_SERVICE '::' serviceFunction ?= FEAT_FUNCTION
	| actor ?= KW_ACTOR
	| KW_ACTOR '::' actorMenu ?= FEAT_MENU
	| KW_ACTOR '::' actorGroup ?= FEAT_GROUP
	| query ?= KW_QUERY
	| view ?= KW_VIEW
	| KW_VIEW '::' viewAction ?= FEAT_ACTION
	| KW_VIEW '::' viewConstructor ?= FEAT_CONSTRUCTOR
	| KW_VIEW '::' viewField ?= FEAT_FIELD
	| KW_VIEW '::' viewGroup ?= FEAT_GROUP
	| KW_VIEW '::' viewLink ?= FEAT_LINK
	| KW_VIEW '::' viewSubmit ?= FEAT_SUBMIT
	| KW_VIEW '::' viewTable ?= FEAT_TABLE
	| KW_VIEW '::' viewTabs ?= FEAT_TABS
	| row ?= KW_ROW
	| KW_ROW '::' rowColumn ?= FEAT_COLUMN
	| service ?= KW_SERVICE
//	| KW_SERVICE '::' serviceAccess ?= FEAT_ACCESS
	;

AnnotationMark
	: '@' declaration = [AnnotationDeclaration | ModelName] ('(' arguments += AnnotationArgument? (',' arguments += AnnotationArgument?)* ')')?
	;

AnnotationArgument
    : declaration = [AnnotationParameterDeclaration | LocalName] '=' (literal = Literal | reference = [AnnotationParameterDeclaration | LocalName])
    ;

EnumLiteralReference
    : {EnumLiteralReference} enumDeclaration = [EnumDeclaration | ModelName] '#' enumLiteral = [EnumLiteral | EnumLiteralName ]    	
    ;

NavigationTarget
//    : EntityFieldDeclaration
//	| EntityRelationDeclaration
//	| EntityDerivedDeclaration
    : EntityMemberDeclaration
    | EntityRelationOppositeInjected
    ;

Literal
	: BooleanLiteral
	| NumberLiteral
	| StringLiteral
	| TemporalLiteral
	| EnumLiteralReference
	;

BooleanLiteral
	: {BooleanLiteral} (CONST_FALSE | isTrue?=CONST_TRUE)
	;

NumberLiteral
	: IntegerLiteral
	| DecimalLiteral
	;

IntegerLiteral hidden()
	: (minus ?= "-" | "+")? value=Natural
	;

DecimalLiteral hidden()
	: (minus ?= "-" | "+")? value=Decimal
	;

StringLiteral
	: {EscapedStringLiteral} value=STRING
	| {RawStringLiteral} value=RAW_STRING
	;

TemporalLiteral
	: {DateLiteral} value=DATE
	| {TimestampLiteral} value=TIMESTAMP
	| {TimeLiteral} value=TIME
	;


 // ************************************
 // For later use
 // ************************************
 
ConstraintDeclaration
	: FEAT_CONSTRAINT Named expression = Expression (throw = ThrowModifier?) SC
	;

ThrowModifier
	: ATTR_THROW ':' value = CreateError
	;
 
/****************************
 *	Error
 ****************************/

ErrorDeclaration
	: KW_ERROR Named (KW_EXTENDS extends = [ErrorDeclaration | ModelName])?
	  (SC | BLOCK_START (fields += ErrorField)* BLOCK_END)
	;

ErrorField
	: FEAT_FIELD referenceType = [PrimitiveDeclaration | ModelName] Named
	  ('=' defaultExpression = Expression)? SC
	;

CreateError
	: errorDeclarationType=[ErrorDeclaration | ModelName] ('(' (parameters+=ThrowParameter (',' parameters+=ThrowParameter)*)? ')')?
	;

ThrowParameter
	: errorFieldType=[ErrorField | LocalName] '=' expession=Literal;


/****************************
 *	Common
 ****************************/

Decimal returns ecore::EBigDecimal
	: NUMBER '.' NUMBER
	;

Natural returns ecore::EBigInteger
	: NUMBER
	;

EnumLiteralName
	: JSLID
    ;

LocalName
	: JSLID
    ;

ModelName
	: JSLID ('::' JSLID)*
    ;

// JSLID : ID;


// TODO: all keywords occurs java error
JSLID : ID
//		| KW_ABSTRACT | KW_ACTOR | KW_ANNOTATION | KW_AS | KW_ENTITY | KW_ENUM | KW_ERROR | KW_EXPORTS | KW_EXTENDS | KW_HUMAN |  KW_IMPORT
//		| KW_MODEL | KW_ON | KW_QUERY | KW_ROW | KW_SERVICE | KW_SYSTEM | KW_TRANSFER | KW_TYPE | KW_VIEW

		| FEAT_ACTION | FEAT_COLUMN | FEAT_CONSTRAINT | FEAT_CONSTRUCTOR | FEAT_DERIVED | FEAT_FIELD | FEAT_FUNCTION
		| FEAT_GROUP | FEAT_IDENTIFIER | FEAT_LINK | FEAT_LITERAL | FEAT_MENU | FEAT_RELATION | FEAT_RESOURCE | FEAT_SUBMIT | FEAT_TABLE | FEAT_TABS
		| FEAT_TEXT
			
		| CONST_BINARY | CONST_BOOLEAN | CONST_BOTTOM | CONST_CENTER | CONST_DATE | CONST_LEFT | CONST_NUMERIC | CONST_RIGHT
		| CONST_STRETCH	| CONST_STRING | CONST_TIME | CONST_TIMESTAMP | CONST_TOP

		| ATTR_CHOICES | ATTR_CLAIM | ATTR_CREATE | ATTR_DETAIL | ATTR_ENABLED | ATTR_FRAME | ATTR_GUARD | ATTR_HIDDEN | ATTR_HORIZONTAL | ATTR_ICON
		| ATTR_IDENTITY | ATTR_LABEL | ATTR_REALM | ATTR_ROWS | ATTR_SCALE | ATTR_TAG
		| ATTR_THROW | ATTR_VERTICAL | ATTR_WIDTH

//		| OTHER_OR | OTHER_XOR | OTHER_AND | OTHER_DIV | OTHER_MOD | OTHER_KB | OTHER_MB | OTHER_GB | OTHER_KIB | OTHER_MIB | OTHER_GIB
	;
			// others
//			| 'regex' | 'precision' | 'scale' | 'lambda'
//			| 'collection' | 'declaration' | 'constant'
//			| 'implies' | 'or' | 'xor' | 'and' | 'div' | 'mod' 
//			| 'kB' | 'MB' | 'GB' | 'KiB' | 'MiB' | 'GiB'
//;

//			// keywords
//JSLID : ID	| KW_ABSTRACT | KW_ACTOR | KW_ANNOTATION | KW_AS | KW_ENTITY | KW_ENUM | KW_ERROR | KW_EXPORTS | KW_EXTENDS | KW_IMPORT
//			| KW_MODEL | KW_ON | KW_QUERY | KW_ROW | KW_SERVICE | KW_TRANSFER | KW_TYPE | KW_VIEW
//
//			// features
//			| FEAT_ACTION | FEAT_COLUMN | FEAT_CONSTRAINT | FEAT_CONSTRUCTOR | FEAT_DERIVED | FEAT_FIELD | FEAT_FUNCTION
//			| FEAT_GROUP | FEAT_IDENTIFIER | FEAT_LINK | FEAT_MENU | FEAT_RELATION | FEAT_REQUIRED | FEAT_SUBMIT | FEAT_TABLE | FEAT_TABS
//
//			// constants
//			| CONST_BINARY | CONST_BOOLEAN | CONST_BOTTOM | CONST_CENTER | CONST_DATE | CONST_LEFT | CONST_NUMERIC | CONST_RIGHT
//			| CONST_STRETCH	| CONST_STRING | CONST_TIME | CONST_TIMESTAMP | CONST_TOP
//
//			// attributes except 'error'
//			| ATTR_CHOICES | ATTR_CLAIM | ATTR_CREATE | ATTR_ENABLED | ATTR_FRAME | ATTR_GUARD | ATTR_HIDDEN | ATTR_HORIZONTAL | ATTR_ICON
//			| ATTR_IDENTITY | ATTR_LABEL | ATTR_REALM | ATTR_ROWS | ATTR_TAG | ATTR_TARGET | ATTR_TEXT | ATTR_THROW | ATTR_VERTICAL | ATTR_WIDTH
//
//			// others
//			| 'regex' | 'precision' | 'scale' | 'lambda'
//			| 'collection' | 'declaration' | 'constant'
//			| 'implies' | 'or' | 'xor' | 'and' | 'div' | 'mod' 
//			| 'kB' | 'MB' | 'GB' | 'KiB' | 'MiB' | 'GiB'
//	       
//	       // 'void' not included
//	       // 'reads', 'maps' are not included
//	       // 'self', 'true', 'false', 'not' are not included
//	;

KW: KW_ABSTRACT | KW_ACTOR | KW_ANNOTATION | KW_AS | KW_ENTITY | KW_ENUM | KW_ERROR | KW_EXPORTS | KW_EXTENDS | KW_HUMAN |  KW_IMPORT
			| KW_MODEL | KW_ON | KW_QUERY | KW_ROW | KW_SERVICE | KW_SYSTEM | KW_TRANSFER | KW_TYPE | KW_VIEW;

FEAT: FEAT_ACTION | FEAT_COLUMN | FEAT_CONSTRAINT | FEAT_CONSTRUCTOR | FEAT_DERIVED | FEAT_FIELD | FEAT_FUNCTION
			| FEAT_GROUP | FEAT_IDENTIFIER | FEAT_LINK | FEAT_LITERAL | FEAT_MENU | FEAT_RELATION | FEAT_RESOURCE | FEAT_SUBMIT | FEAT_TABLE | FEAT_TABS
			| FEAT_TEXT ;
			
CONST: CONST_BINARY | CONST_BOOLEAN | CONST_BOTTOM | CONST_CENTER | CONST_DATE | CONST_LEFT | CONST_NUMERIC | CONST_RIGHT
			| CONST_STRETCH	| CONST_STRING | CONST_TIME | CONST_TIMESTAMP | CONST_TOP;

ATTR: ATTR_CHOICES | ATTR_CLAIM | ATTR_CREATE | ATTR_DETAIL | ATTR_ENABLED | ATTR_FRAME | ATTR_GUARD | ATTR_HIDDEN | ATTR_HORIZONTAL | ATTR_ICON
			| ATTR_IDENTITY | ATTR_LABEL | ATTR_REALM | ATTR_REQUIRED | ATTR_ROWS | ATTR_TAG
			| ATTR_THROW | ATTR_VERTICAL | ATTR_WIDTH;

OTHER: OTHER_OR | OTHER_XOR | OTHER_AND | OTHER_DIV | OTHER_MOD | OTHER_KB | OTHER_MB | OTHER_GB | OTHER_KIB | OTHER_MIB | OTHER_GIB;


/****************************
 *	Terminals
 ****************************/

terminal KW_ABSTRACT:	'abstract';
terminal KW_ACTOR:		'actor';
terminal KW_ANNOTATION:	'annotation';
terminal KW_AS:			'as';
terminal KW_ENTITY:		'entity';
terminal KW_ENUM:		'enum';
terminal KW_ERROR:		'error';
terminal KW_EXPORTS:	'exports';
terminal KW_EXTENDS:	'extends';
terminal KW_HUMAN:		'human';
terminal KW_IMPORT:		'import';
terminal KW_MAPS:		'maps';
terminal KW_MODEL:		'model';
terminal KW_ON:			'on';
terminal KW_QUERY:		'query';
terminal KW_ROW:		'row';
terminal KW_SERVICE:	'service';
terminal KW_SYSTEM:		'system';
terminal KW_TRANSFER:	'transfer';
terminal KW_TYPE:		'type';
terminal KW_VIEW:		'view';

terminal FEAT_ACTION:		'action';
terminal FEAT_COLUMN:		'column';
terminal FEAT_CONSTRAINT:	'constraint';
terminal FEAT_CONSTRUCTOR:	'constructor';
terminal FEAT_DERIVED:		'derived';
terminal FEAT_DESTRUCTOR:	'destructor';
terminal FEAT_FIELD:		'field';
terminal FEAT_FUNCTION:		'function';
terminal FEAT_GROUP:		'group';
terminal FEAT_IDENTIFIER:	'identifier';
terminal FEAT_INITIALIZER:	'initializer';
terminal FEAT_LINK:			'link';
terminal FEAT_LITERAL:		'literal';
terminal FEAT_MENU:			'menu';
terminal FEAT_RELATION:		'relation';
terminal FEAT_RESOURCE:		'resource';
terminal FEAT_SUBMIT:		'submit';
terminal FEAT_TABLE:		'table';
terminal FEAT_TABS:			'tabs';
terminal FEAT_TEXT:			'text';

terminal CONST_BINARY:		'binary';
terminal CONST_BOOLEAN:		'boolean';
terminal CONST_BOTTOM:		'bottom';
terminal CONST_CENTER:		'center';
terminal CONST_DATE:		'date';
terminal CONST_FALSE:		'false';
terminal CONST_LEFT:		'left';
terminal CONST_NUMERIC:		'numeric';
terminal CONST_RIGHT:		'right';
terminal CONST_STRETCH:		'stretch';
terminal CONST_STRING:		'string';
terminal CONST_TIME:		'time';
terminal CONST_TIMESTAMP:	'timestamp';
terminal CONST_TOP:			'top';
terminal CONST_TRUE:		'true';
terminal CONST_VOID:		'void';

terminal ATTR_CHOICES:		'choices';
terminal ATTR_CLAIM:		'claim';
terminal ATTR_CREATE:		'create';
terminal ATTR_DETAIL:		'detail';
terminal ATTR_ENABLED:		'enabled';
terminal ATTR_FRAME:		'frame';
terminal ATTR_GUARD:		'guard';
terminal ATTR_HIDDEN:		'hidden';
terminal ATTR_HORIZONTAL:	'horizontal';
terminal ATTR_ICON:			'icon';
terminal ATTR_IDENTITY:		'identity';
terminal ATTR_LABEL:		'label';
terminal ATTR_MAXFILESIZE:	'max-file-size';
terminal ATTR_MAXSIZE:		'max-size';
terminal ATTR_MINSIZE:		'min-size';
terminal ATTR_MIMETYPE:		'mime-type';
terminal ATTR_OPPOSITE:		'opposite';
terminal ATTR_OPPOSITEADD:	'opposite-add';
terminal ATTR_PRECISION:	'precision';
terminal ATTR_REALM:		'realm';
terminal ATTR_REGEX:		'regex';
terminal ATTR_REQUIRED:		'required';
terminal ATTR_ROWS:			'rows';
terminal ATTR_SCALE:		'scale';
terminal ATTR_TAG:			'tag';
// terminal ATTR_TEXT:			'text';
terminal ATTR_THROW:		'throw';
terminal ATTR_VERTICAL:		'vertical';
terminal ATTR_WIDTH:		'width';

terminal OTHER_LAMBDA:		'lambda';
terminal OTHER_COLLECTION:	'collection';
terminal OTHER_DECLARATION:	'declaration';
terminal OTHER_CONSTANT:	'constant';
terminal OTHER_IMPLIES:		'implies';

terminal OTHER_OR:		'or';
terminal OTHER_XOR:		'xor';
terminal OTHER_AND:		'and';
terminal OTHER_DIV:		'div';
terminal OTHER_MOD:		'mod';
terminal OTHER_KB:		'kB';
terminal OTHER_MB:		'MB';
terminal OTHER_GB:		'GB';
terminal OTHER_KIB:		'KiB';
terminal OTHER_MIB:		'MiB';
terminal OTHER_GIB:		'GiB';


terminal BLOCK_START
	: '{'
	;

terminal BLOCK_END
	: '}' WS? SC?
	;

terminal fragment DIGIT
	: '0'..'9'
	;

terminal fragment ALPHABET
    : 'a'..'z' | 'A'..'Z'    	
    ;

terminal fragment NEWLINE
    : '\n' | '\r'   	
    ;

terminal TIMESTAMP
	: '`' DIGIT+ '-' DIGIT DIGIT '-' DIGIT DIGIT 'T' DIGIT DIGIT ':' DIGIT DIGIT (':' DIGIT DIGIT ('.' DIGIT+)?)? ('Z' | ('+' | '-') DIGIT DIGIT (':' DIGIT DIGIT)?)?'`'
	;

terminal TIME
	: '`' DIGIT DIGIT ':' DIGIT DIGIT (':' DIGIT DIGIT ('.' DIGIT+ )? )? '`'
	;

terminal DATE
	: '`' DIGIT+ '-' DIGIT DIGIT '-' DIGIT DIGIT '`'
	;
 
terminal NUMBER
	: DIGIT+
	;

terminal ID
	: ALPHABET (ALPHABET | DIGIT)* | '`' ALPHABET (ALPHABET | DIGIT)* '`'
	;

terminal STRING
	: '"' ('\\' ('t'|'n'|'f'|'r'|'"'|'\\') | !('\\' | '"' | NEWLINE))* '"'
	;

terminal RAW_STRING
	: 'r"' (!('"' | NEWLINE))* '"'
	;
 
terminal ML_COMMENT
	: '/*'->'*/'
	;

terminal SL_COMMENT
	: '//' !(NEWLINE)*
	;

terminal SC
	: (';' WS?)+
	;

terminal WS
	: (' '|'\t'|'\r'|'\n')+
	;
