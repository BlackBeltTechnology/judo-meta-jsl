grammar hu.blackbelt.judo.meta.jsl.JslDsl hidden(WS, NL, SL_COMMENT, ML_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate jsldsl "http://www.blackbelt.hu/judo/meta/jsl/JslDsl"
 
ModelDeclaration returns ModelDeclaration hidden(WS, SL_COMMENT, ML_COMMENT)
    : {ModelDeclaration} 'model' name = ModelName
      imports += ModelImport*
	  (declarations += Declaration)*
	  NL*
    ;

//ModelImport
//    : =>NL+ 'import' importedModel = [ModelDeclaration] ('as' alias = ID)?
//	;

ModelImport
    : NL+ 'import' modelName = ModelImportDeclaration
	;

ModelImportDeclaration
    : importName = ModelName ('as' alias = ID)?
	;

Declaration
	: NL+ (TypeDeclaration
	| ErrorDeclaration
	| QueryDeclaration )
	;

TypeDeclaration
	: PrimitiveDeclaration
	| ClassDeclaration
	;

PrimitiveDeclaration
	: DataTypeDeclaration
	| EnumDeclaration
    ;

ClassDeclaration
	: EntityDeclaration
	;

/****************************
 *	Entity
 ****************************/

EntityDeclaration
	: {EntityDeclaration}
	  'entity' isAbstract ?= 'abstract'? name = ID
	  ('extends' extends += [EntityDeclaration | LocalName] (',' extends += [EntityDeclaration | LocalName])*)?
	  BLOCK_START (members += EntityMemberDeclaration)* BLOCK_END
	;

QueryDeclaration
	: 'query' (referenceType = [EntityDerivedSingleType | LocalName] '[]') name = ID
	  ('(' parameters += QueryDeclarationParameter (',' parameters += QueryDeclarationParameter)* ')')
	  "=" expression = Expression
	;

EntityMemberDeclaration
	: NL+ (EntityFieldDeclaration
	| EntityIdentifierDeclaration
	| EntityRelationDeclaration
	| EntityDerivedDeclaration
	| EntityQueryDeclaration
	| ConstraintDeclaration)
	;

ConstraintDeclaration
	: 'constraint' expression = Expression ('onerror' error = CreateError)?
	;

EntityFieldDeclaration
	: ('field')? (isRequired ?= 'required'? referenceType = [EntityFieldSingleType | LocalName] isMany ?= '[]'?) name = ID
	  (defaultExpression = DefaultExpressionType)?
	;


DefaultExpressionType
	: {DefaultExpressionType} '=' expression = (
		Literal 
		| EnumLiteralReference 
		| NavigationBase 
		| ParenthesizedExpression
		| CreateExpression
	)
	;

EntityFieldSingleType 
	: PrimitiveDeclaration
	| EntityDeclaration
	;

EntityIdentifierDeclaration
	: 'identifier' isRequired ?= 'required'? referenceType = [PrimitiveDeclaration | LocalName] name = ID
	  (defaultExpression = DefaultExpressionType)?
	;

EntityDerivedDeclaration
	: 'derived' (referenceType = [EntityDerivedSingleType | LocalName] isMany ?= '[]'?) name = ID
	  "=" expression = Expression
	;

EntityQueryDeclaration
	: 'query' (referenceType = [EntityDerivedSingleType | LocalName] '[]') name = ID
	  ('(' parameters += QueryDeclarationParameter (',' parameters += QueryDeclarationParameter)* ')')
	  "=" expression = Expression
	;

EntityDerivedSingleType
	: PrimitiveDeclaration
	| EntityDeclaration
	;

QueryDeclarationParameter
	: referenceType = [PrimitiveDeclaration | LocalName] name = ID "=" default = Literal
	;

EntityRelationDeclaration
	: 'relation' (isRequired ?= 'required'? referenceType = [EntityDeclaration | LocalName] isMany ?= '[]'?) name = ID
	  opposite = EntityRelationOpposite?
	;

EntityRelationOpposite
	: 'opposite-add' oppositeName = ID (isMany ?= '[]')?
	| 'opposite' oppositeType = [EntityRelationDeclaration | LocalName]
	;


/****************************
 *	Error
 ****************************/

ErrorDeclaration
	: 'error' name = ID ('extends' extends = [ErrorDeclaration])?
	  BLOCK_START (fields += ErrorField)* BLOCK_END
	;

ErrorField
	: NL+ 'field' isRequired ?= 'required'? referenceType = [PrimitiveDeclaration | LocalName] name = ID
	  (defaultExpression = DefaultExpressionType)?
	;

CreateError
	: errorDeclarationType=[ErrorDeclaration | LocalName] ('(' (parameters+=ThrowParameter (',' parameters+=ThrowParameter)*)? ')')?
	;

ThrowParameter
	: errorFieldType=[ErrorField | LocalName] '=' expession=Expression;



/****************************
 *	Datatype
 ****************************/

    
DataTypeDeclaration
    : 'type'
      (
		  primitive = 'boolean' name = ID
		| primitive = 'binary' name = ID (mimeTypes = ModifierMimeTypes? & maxFileSize = ModifierMaxFileSize?) 
		| primitive = 'string' name = ID (maxLength = ModifierMaxLength & regex = ModifierRegex?)
		| primitive = 'numeric' name = ID (precision = ModifierPrecision & scale = ModifierScale)
		| primitive = 'date' name = ID
		| primitive = 'time' name = ID
		| primitive = 'timestamp' name = ID
      )
    ;


/****************************
 *	Enum
 ****************************/

EnumDeclaration
    : 'enum' name = ID
      BLOCK_START (literals += EnumLiteral)+ BLOCK_END
    ;

EnumLiteral
    : NL+ name = ID '=' value = INTEGER
    ;


ModifierMaxLength
	: 'max-length' maxLength = INTEGER
	;

ModifierRegex
	: 'regex' regex = StringLiteral
	;

ModifierPrecision
	: 'precision' precision = INTEGER
	;

ModifierScale
	: 'scale' scale = INTEGER
	;


ModifierMimeTypes
	: 'mime-types' mimeTypes+=MIME_TYPE (',' mimeTypes += MIME_TYPE)*
	;

ModifierMaxFileSize
	: 'max-file-size' maxFileSize = INTEGER
	;
	

/****************************
 *	Expression
 ***************************/

Expression returns Expression hidden(WS, CONT_NL, SL_COMMENT, ML_COMMENT)
	: SwitchExpression
	;

MultilineExpression returns Expression hidden(WS, NL, CONT_NL, SL_COMMENT, ML_COMMENT)
	: SwitchExpression
	;

// right associative rule
SwitchExpression returns Expression
	: ImpliesExpression (=> ({TernaryOperation.condition=current} '?')
	  thenExpression=SwitchExpression ':'
      elseExpression=SwitchExpression)?
    ;

ImpliesExpression returns Expression
	: OrExpression (=> ({BinaryOperation.leftOperand=current} operator='implies') rightOperand=OrExpression)*
	;

OrExpression returns Expression
	: XorExpression (=> ({BinaryOperation.leftOperand=current} operator='or') rightOperand=XorExpression)*
	;

XorExpression returns Expression
	: AndExpression (=> ({BinaryOperation.leftOperand=current} operator='xor') rightOperand=AndExpression)*
	;

AndExpression returns Expression
	: EqualityExpression (=> ({BinaryOperation.leftOperand=current} operator='and') rightOperand=EqualityExpression)*
	;

EqualityExpression returns Expression
	: RelationalExpression (=> ({BinaryOperation.leftOperand=current} operator=('!='|'==')) rightOperand=RelationalExpression)*
	;

RelationalExpression returns Expression
	: AdditiveExpression (=> ({BinaryOperation.leftOperand=current} operator=('>=' | '<=' | '>' | '<')) rightOperand=AdditiveExpression)*
	;

AdditiveExpression returns Expression
	: MultiplicativeExpression (=> ({BinaryOperation.leftOperand=current} operator=('+'|'-')) rightOperand=MultiplicativeExpression)*
	;

MultiplicativeExpression returns Expression
	: ExponentExpression (=> ({BinaryOperation.leftOperand=current} operator=('*' | '/' | 'div' | 'mod')) rightOperand=ExponentExpression)*
	;

ExponentExpression returns Expression
	: SpawnOperation (=> ({BinaryOperation.leftOperand=current} operator='^') rightOperand=SpawnOperation)*
	;

SpawnOperation returns Expression
	: UnaryOperation (=> ({SpawnOperation.operand=current} 'as' spawnTargetType=LocalName))?
	;

UnaryOperation returns Expression
	: {UnaryOperation} operator=('not' | '-') operand=FunctionedExpression
    | FunctionedExpression
    ;

FunctionedExpression returns Expression
	: NavigationExpression ({FunctionedExpression.operand=current} functionCall=FunctionCall)?
	;

NavigationExpression returns Expression
	: PrimaryExpression ({NavigationExpression.base=current} features+=Feature+)?
    | NavigationBase
    ;

NavigationBase returns NavigationExpression
	: {NavigationExpression} (isSelf = 'self' | navigationBaseType = [NavigationBaseReference | LocalName]) (features+=Feature* | enumValue = [ EnumLiteralReference ])		
	;

NavigationBaseReference
	: EntityDeclaration
	| QueryDeclaration
	| LambdaVariable
	| QueryDeclarationParameter
	;

EnumLiteralReference returns Expression
    : {EnumLiteralReference} enumDeclaration = [EnumDeclaration | LocalName] '#' enumLiteral = [EnumLiteral | EnumLiteralName ]    	
    ;

FunctionCall
	: {FunctionCall} '!' function=Function features+=Feature* call=FunctionCall?
	;

Feature
    : {Feature} '.' member = NavigationFeature
    ;

NavigationFeature returns Feature
	: navigationDeclarationType = [NavigationDeclaration | LocalName ] ('(' (parameters+=QueryParameter (',' parameters+=QueryParameter)*)? ')')?
	;

NavigationDeclaration
    : LambdaVariable
    | EntityMemberDeclaration
    ;

QueryParameter
	:  queryParameterType=[QueryDeclarationParameter | LocalName] '=' (literal = Literal | parameter = [QueryDeclarationParameter | LocalName])     // expression=MultilineExpression
	;

ParenthesizedExpression returns Expression
	: {ParenthesizedExpression} '(' =>expression = MultilineExpression ')'
	;

PrimaryExpression returns Expression
	: ParenthesizedExpression
	| CreateExpression
	| Literal
	;


CreateExpression returns Expression
	: {CreateExpression} 'new'? createType=[ClassDeclaration | LocalName]
	  (   '(' (assignments+=CreateParameter (',' assignments+=CreateParameter)*)? ')'
	  	| '[' (creates += MultilineExpression (',' creates += MultilineExpression)*)? ']'
	  )
	;

CreateParameter:
    name=ID '=' right=MultilineExpression;


Function returns Function
	: name=ID '(' parameterDeclaration = FunctionParameterDeclaration? ')'
    ;

FunctionParameterDeclaration
	: LambdaFunctionParameters
	| ParametrizedFunctionParameters
	;

LambdaFunctionParameters
    : lambdaArgument=LambdaVariable '|' expression = Expression
	;

ParametrizedFunctionParameters
	: parameters+=FunctionParameter (',' parameters+=FunctionParameter)*
    ;
    
LambdaVariable
    : {LambdaVariable} name=ID    	
    ;

FunctionParameter
	: {FunctionParameter} expression=MultilineExpression
	;

Literal returns Expression
	: BooleanLiteral
	| NumberLiteral
	| StringLiteral
	| TemporalLiteral
	;

BooleanLiteral returns Expression
	: {BooleanLiteral} ('false' | isTrue?='true')
	;

NumberLiteral returns Expression
	: {IntegerLiteral} value=INTEGER
	| {DecimalLiteral} value=DECIMAL
	;

StringLiteral returns Expression
	: {EscapedStringLiteral} value=STRING
	| {RawStringLiteral} value=RAW_STRING
	;

TemporalLiteral returns Expression
	: {DateLiteral} value=DATE
	| {TimeStampLiteral} value=TIMESTAMP
	| {TimeLiteral} value=TIME
	;


EnumLiteralName
	: ID
    ;

LocalName
	: (ID '::')? ID
    ;

ModelName
	: ID('::' ID)*
    ;

/****************************
 *	Terminals
 ****************************/

BLOCK_START
	: NL* '{'
	;

BLOCK_END
	: NL* '}'
	;


terminal TIMESTAMP
	: '`' DIGIT+ '-' DIGIT DIGIT '-' DIGIT DIGIT 'T' DIGIT DIGIT ':' DIGIT DIGIT (':' DIGIT DIGIT ('.' DIGIT+ )? )? ('Z' | ('+' | '-') DIGIT DIGIT ':' DIGIT DIGIT )'`'
	;

terminal TIME
	: '`' DIGIT DIGIT ':' DIGIT DIGIT (':' DIGIT DIGIT ('.' DIGIT+ )? )? '`'
	;

terminal DATE
	: '`' DIGIT+ '-' DIGIT DIGIT '-' DIGIT DIGIT '`'
	;

terminal DIGIT
	: '0'..'9'
	;

terminal DECIMAL returns ecore::EBigDecimal
	: '-'? DIGIT+ '.' DIGIT+
	;

terminal INTEGER returns ecore::EBigInteger
	: '-'? DIGIT+
	;

terminal ID
	: '\\'? ('a'..'z' | 'A'..'Z') ('a'..'z' | 'A'..'Z' | '0'..'9')*
	;

terminal STRING
	: '"' ('\\' ('t'|'n'|'f'|'r'|'"'|'\\') | !('\\' | '"' | '\n' | '\r'))* '"'
	;

terminal RAW_STRING
	: 'r"' (!('"' | '\n' | '\r'))* '"'
	;

terminal MIME_TYPE
    : 'm"' ('a'..'z' | 'A'..'Z')+ '/' ('*' | ('a'..'z' | 'A'..'Z')+ ('.' ('a'..'z' | 'A'..'Z')+)* ) '"'
	;

terminal CONT_NL
	: '\\' (' ' | '\t')* '\r'? '\n'
	;
 
terminal ML_COMMENT
	: '/*'->'*/'
	;

terminal SL_COMMENT
	: '//' !('\n' | '\r')*
	;

terminal WS
	: (' ' | '\t')+
	;

terminal NL
	: '\r' | '\n' | ';'
	;

terminal ANY_OTHER
	: .
	;
